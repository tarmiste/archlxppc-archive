# $Id: PKGBUILD 104455 2011-01-03 05:46:57Z eric $
# x86 Maintainer: Eric Belanger <eric@archlinux.org>
pkgname=alpine
pkgver=2.00
pkgrel=13
_patchlevel=84
pkgdesc="The Apache-licensed PINE (a tool for reading, sending, and managing electronic messages)"
arch=('i686' 'x86_64' 'ppc')
url="http://www.washington.edu/alpine/"
license=('APACHE')
depends=('libldap' 'heimdal' 'gettext')
optdepends=('aspell: for spell-checking support')
provides=('pine')
conflicts=('pine')
replaces=('pine')
options=('!makeflags')
source=(ftp://ftp.cac.washington.edu/${pkgname}/${pkgname}.tar.bz2 \
        http://staff.washington.edu/chappa/alpine/patches/alpine-${pkgver}/all_${_patchlevel}.patch.gz \
        2.00-lpam.patch CVE-2008-5514.patch)

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 < ../all_${_patchlevel}.patch
  patch -p1 < ../2.00-lpam.patch
  patch -p1 < ../CVE-2008-5514.patch
  ./configure --prefix=/usr --without-passfile --without-tcl \
    --disable-shared --with-system-pinerc=/etc/alpine.d/pine.conf \
    --with-system-fixed-pinerc=/etc/alpine.d/pine.conf.fixed
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
md5sums=('84e44cbf71ed674800a5d57eed9c1c52'
         '5e3798e4902bd606b51dc36cd2d8fec2'
         'cd3911c16fc6a072e853c0ccfc35857c'
         '1b52a54a656979116c09fb1d948a4325')
sha1sums=('dcbd3c5419954f484ccf706feaba31ce48cdebc4'
          'f93160160d338d96e470e1c0cf3567a724a0b9a2'
          '1b39525f91ebd5a9de5a1e04f5554f6fa5f58ae3'
          'bc61d76a237ff42b00b3f60f2e6fc5c45e261dbb')
