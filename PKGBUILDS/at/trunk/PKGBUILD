# $Id$
# Maintainer: Judd Vinet <jvinet@zeroflux.org>
# Contributor: Todd Musall <tmusall@comcast.net>

pkgname=at
pkgver=3.1.13
pkgrel=1
pkgdesc="AT and batch delayed command scheduling utility and daemon"
arch=('i686' 'x86_64' 'ppc')
url="http://packages.qa.debian.org/a/at.html"
license=('GPL')
depends=('pam')
makedepends=('ssmtp')
backup=('etc/at.deny')
source=(http://ftp.debian.org/debian/pool/main/a/at/at_${pkgver}.orig.tar.gz
        atd)
md5sums=('1da61af6c29e323abaaf13ee1a8dad79'
         '35574e34829e7df07a3269d4ab646a29')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --mandir=/usr/share/man  \
              --with-jobdir=/var/spool/atd \
              --with-atspool=/var/spool/atd
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make IROOT="${pkgdir}" docdir=/usr/share/doc install
  install -D -m755 "${srcdir}/atd" "${pkgdir}/etc/rc.d/atd"
}
