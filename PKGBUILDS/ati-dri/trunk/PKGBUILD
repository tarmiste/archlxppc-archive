# $Id: PKGBUILD,v 1.3 2009/04/28 13:57:48 kth5 Exp $
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Alexander Baldeck <alexander@archlinux.org>
pkgname=ati-dri
pkgver=7.4.1
pkgrel=1
pkgdesc="DRI drivers for ATI/AMD videochips"
arch=(i686 x86_64 ppc)
url="http://xorg.freedesktop.org/"
license=('MIT')
depends=('libdrm>=2.4.5-3' 'libgl>=7.4.1' 'expat>=2.0.1')
makedepends=('pkgconfig' 'libxfixes' 'libxdamage' 'libxxf86vm' 'libxext' 'mesa>=7.4.1' 'xorg-headers')
conflicts=('xf86-video-ati<6.9.0-6')
options=('!libtool')
source=(http://downloads.sourceforge.net/sourceforge/mesa3d/MesaLib-${pkgver}.tar.bz2
       mesa-7.1-link-shared.patch
       LICENSE)

build() {
  cd "${srcdir}/Mesa-${pkgver}"
  patch -Np1 -i "${srcdir}/mesa-7.1-link-shared.patch" || return 1
  ./configure --prefix=/usr \
    --with-dri-driverdir=/usr/lib/xorg/modules/dri \
    --with-dri-drivers=radeon,r200,r300 \
    --enable-glx-tls \
    --with-driver=dri \
    --enable-xcb \
    --disable-glu \
    --disable-glut \
    --disable-glw || return 1
  make || return 1
  cd src/mesa/drivers/dri || return 1
  make DESTDIR="${pkgdir}" install || return 1
  install -D -m644 "${srcdir}/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  rm -f "${pkgdir}/usr/lib/xorg/modules/dri/libdricore.so"
  rm -rf "${pkgdir}/usr/include"
  rm -rf "${pkgdir}/usr/lib/pkgconfig"
}
md5sums=('423260578b653818ba66c2fcbde6d7ad'
         'f0baa948d9810f268413111ee439d24b'
         '7ba429de367a2b3d273d2230cc705771')
