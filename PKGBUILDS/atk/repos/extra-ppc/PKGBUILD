# $Id$
# x86 Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=atk
pkgver=2.3.95
pkgrel=1
pkgdesc="A library providing a set of interfaces for accessibility"
arch=(i686 x86_64 'ppc')
license=('LGPL')
depends=('glib2')
makedepends=('gobject-introspection')
options=('!libtool')
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
url='http://www.gtk.org/'
sha256sums=('a4d03a87f773b699fb88935296185bbe723d2d6eac9cc453a2f68a8b822b4540')

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
