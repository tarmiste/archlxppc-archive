# $Id: PKGBUILD 145658 2011-12-26 12:00:09Z andrea $
# x86 Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=attica
pkgver=0.3.0
pkgrel=1
pkgdesc='A Qt library that implements the Open Collaboration Services API'
arch=('ppc' 'i686' 'x86_64')
url='https://projects.kde.org/projects/kdesupport/attica'
license=('LGPL')
depends=('qt')
makedepends=('cmake')
source=("http://download.kde.org/stable/${pkgname}/${pkgname}-${pkgver}.tar.bz2")
md5sums=('5a77f678422e7a52e3a3abb2d7ad0499')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}/build"
  make DESTDIR="${pkgdir}" install
}
