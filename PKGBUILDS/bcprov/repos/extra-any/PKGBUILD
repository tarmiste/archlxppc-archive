# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=bcprov
pkgver=1.46
pkgrel=2
pkgdesc="Java cryptography APIs (Bouncy Castle)"
arch=('any')
url="http://www.bouncycastle.org/java.html"
license=('custom')
depends=('java-runtime')
makedepends=('java-environment' 'junit')
source=("http://www.bouncycastle.org/download/bcprov-jdk16-${pkgver/./}.tar.gz")
md5sums=('944374e9b57c9f7f6751d9c65ff62374')

build() {
  cd "${srcdir}/bcprov-jdk16-${pkgver/./}"
  jar xf src.zip

  mkdir classes
  find . -name '*.java' > src.list
  javac -encoding ISO-8859-1 -cp /usr/share/java/junit.jar -d classes @src.list

  cd classes
  find . -path '*test/*.class' -delete
  find . -depth -type d -empty -delete
  jar cf ../bcprov.jar *
}

package() {
  cd "${srcdir}/bcprov-jdk16-${pkgver/./}"
  install -m755 -d "${pkgdir}/usr/share/java"
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 bcprov.jar "${pkgdir}/usr/share/java/"
  install -m644 LICENSE.html "${pkgdir}/usr/share/licenses/${pkgname}/"
}
