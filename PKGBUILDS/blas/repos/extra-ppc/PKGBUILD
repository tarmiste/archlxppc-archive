# $Id: PKGBUILD 130989 2011-07-09 12:22:58Z ronald $
# x86 Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: damir <damir@archlinux.org>
# Contributor: Jason Taylor <jftaylor21@gmail.com>

pkgname=blas
pkgver=3.4.0
pkgrel=1
url="http://www.netlib.org/blas"
makedepends=('gcc-fortran')
depends=('gcc-libs')
pkgdesc="Basic Linear Algebra Subprograms"
arch=('ppc' 'i686' 'x86_64')
license=("custom")
source=(http://www.netlib.org/lapack/lapack-${pkgver}.tgz
        Makefile.blas
        LICENSE.blas)
sha1sums=('910109a931524f8dcc2734ce23fe927b00ca199f'
          '85996f56c13991fc69481c0e88ef905a0dcefb49'
          '2491a151a37f0162b25fc4e4e9a8ac444b574a76')
	
build() {
  cd "${srcdir}/lapack-${pkgver}"
  cp -f "${srcdir}/Makefile.blas" BLAS/SRC/Makefile 
  cp -f INSTALL/make.inc.gfortran make.inc

  export FC=gfortran

  pushd BLAS/SRC
  make clean
  FFLAGS="${CFLAGS/-O?/-O0}" make dcabs1.o
  FFLAGS="${CFLAGS} -fPIC" CFLAGS="${CFLAGS} -fPIC" make shared
  cp libblas.so.${pkgver} "${srcdir}/lapack-${pkgver}/"

  popd
  ln -s libblas.so.${pkgver} libblas.so

}

package_blas() {
  install -m755 -d "${pkgdir}/usr/lib"
  install -m755 "${srcdir}/lapack-${pkgver}/libblas.so.${pkgver}" \
    "${pkgdir}/usr/lib/"
  ln -sf libblas.so.${pkgver} "${pkgdir}/usr/lib/libblas.so"
  ln -sf libblas.so.${pkgver} "${pkgdir}/usr/lib/libblas.so.3"
  
  install -m755 -d "${pkgdir}/usr/share/licenses/blas"
  install -m644 "${srcdir}/LICENSE.blas" \
    "${pkgdir}/usr/share/licenses/blas/LICENSE"
}

