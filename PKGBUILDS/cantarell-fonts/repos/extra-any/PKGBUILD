# $Id: PKGBUILD 140841 2011-10-20 05:41:42Z ibiru $
# x86 Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>

pkgname=cantarell-fonts
pkgver=0.0.7
pkgrel=1
pkgdesc="Cantarell font family for the GNOME desktop"
arch=('any')
url="http://abattis.org/cantarell/"
license=('GPL2')
depends=('fontconfig')
install=cantarell-fonts.install
source=(http://download.gnome.org/sources/$pkgname/${pkgver%.*}/$pkgname-${pkgver}.tar.xz)
sha256sums=('6a21f841d28de444deee201a25e5cbc129fde14d633c547a7d36941437f72090')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="${pkgdir}" install

  mkdir -p "$pkgdir/etc/fonts/conf.d"
  mv "$pkgdir/usr/share/fontconfig/conf.avail" "$pkgdir/etc/fonts/conf.avail"
  rmdir "$pkgdir/usr/share/fontconfig"
  ln -s ../conf.avail/31-cantarell.conf "$pkgdir/etc/fonts/conf.d"
}

# vim:set ts=2 sw=2 et:
