# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=cantarell-fonts
pkgver=0.0.8
pkgrel=1
pkgdesc="Cantarell font family for the GNOME desktop"
arch=('any')
url="http://abattis.org/cantarell/"
license=('GPL2')
depends=('fontconfig')
install=cantarell-fonts.install
source=(http://download.gnome.org/sources/$pkgname/${pkgver%.*}/$pkgname-${pkgver}.tar.xz)
sha256sums=('e5468861cee84c034dfc53ed9836a360bea45767873dd7840fe403a06bcc61fb')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="${pkgdir}" install

  mkdir -p "$pkgdir/etc/fonts/conf.d"
  mv "$pkgdir/usr/share/fontconfig/conf.avail" "$pkgdir/etc/fonts/conf.avail"
  rmdir "$pkgdir/usr/share/fontconfig"
  ln -s ../conf.avail/31-cantarell.conf "$pkgdir/etc/fonts/conf.d"
}

# vim:set ts=2 sw=2 et:
