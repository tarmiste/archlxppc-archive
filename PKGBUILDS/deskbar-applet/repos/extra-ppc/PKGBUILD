# $Id: PKGBUILD 101145 2010-11-28 13:08:43Z jgc $
# x86 Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=deskbar-applet
pkgver=2.32.0
pkgrel=4
pkgdesc="An all-in-one search bar for the GNOME panel"
arch=('ppc' i686 x86_64)
url="http://raphael.slinckx.net/deskbar/"
license=('GPL')
depends=('gnome-desktop>=2.32.0' 'gnome-python>=2.28' 'python-gnomedesktop>=2.32' 'python-gnomekeyring>=2.32' 'python-wnck>=2.32' 'python-gnomeapplet>=2.32' 'gnome-panel-bonobo>=2.32.0' 'dbus-python>=0.83.0')
makedepends=('evolution-data-server>=2.32.0' 'gettext' 'intltool' 'pkg-config' 'gnome-doc-utils>=0.20.1' 'gnome-python-desktop>=2.32.0')
options=('!libtool' '!emptydirs')
groups=('gnome-extra')
install=deskbar-applet.install
source=(http://ftp.gnome.org/pub/GNOME/sources/deskbar-applet/2.32/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('123f2231ac8449f8376030c1b25394f9bab18d5d219292afc6f27eda77d4e684')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed '/^modulesdir=/ s/@libdir@/@libexecdir@/' -i data/deskbar-applet.pc.in
  PYTHON=python2 ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --libexecdir=/usr/lib \
      --disable-scrollkeeper --enable-evolution \
      --disable-schemas-install
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install

  sed -i "s|#!/usr/bin/env python$|#!/usr/bin/env python2|" \
    "$pkgdir/usr/lib/deskbar-applet/deskbar-applet"

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain deskbar-applet ${pkgdir}/etc/gconf/schemas/*.schemas
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
