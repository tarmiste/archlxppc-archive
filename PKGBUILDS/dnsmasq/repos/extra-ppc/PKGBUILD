# $Id$
# x86 Maintainer: Dave Reisner <dreisner@archlinux.org>
# Contributor: Paul Mattal <paul@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=dnsmasq
pkgver=2.60
pkgrel=3
pkgdesc="Lightweight, easy to configure DNS forwarder and DHCP server"
url="http://www.thekelleys.org.uk/dnsmasq/doc.html"
arch=('i686' 'x86_64' 'ppc')
license=('GPL')
depends=('glibc' 'dbus-core')
install=$pkgname.install
backup=('etc/dnsmasq.conf'
        'etc/conf.d/dnsmasq')
source=("http://www.thekelleys.org.uk/$pkgname/$pkgname-$pkgver.tar.gz"
        'dnsmasq.confd'
        'rc.dnsmasq'
        'dnsmasq.service')
md5sums=('5a09908e02724d759ac60d9cafd27de8'
         '66479e99123faeab83ebaed709ef95b5'
         '819fbdf6440d710616e6df5f8ca9cdba'
         '631d8349acbed1b01d4e11d9f42fdb7d')

build() {
  cd "$pkgname-$pkgver"

  # link against dbus. this ugliness is needed to ensure that the
  # compile time opts report properly on startup. yuck.
  sed -i '/^#ifdef DNSMASQ_COMPILE_OPTS/ {
    i#define HAVE_DBUS
  }' src/config.h

  make
}

package() {
  cd "$pkgname-$pkgver"

  make BINDIR=/usr/bin PREFIX=/usr DESTDIR="$pkgdir" install

  install -Dm755 "$srcdir"/rc.dnsmasq "$pkgdir"/etc/rc.d/dnsmasq
  install -Dm644 "$srcdir"/dnsmasq.confd "$pkgdir/etc/conf.d/dnsmasq"

  install -Dm644 "dbus/dnsmasq.conf" "$pkgdir"/etc/dbus-1/system.d/dnsmasq.conf
  install -Dm644 "dnsmasq.conf.example" "$pkgdir"/etc/dnsmasq.conf
  install -Dm644 "$srcdir/dnsmasq.service" "$pkgdir"/usr/lib/systemd/system/dnsmasq.service
}

# vim: ts=2 sw=2 et ft=sh
