# $Id: PKGBUILD,v 1.3 2010/03/09 17:09:52 kth5 Exp $
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Contributor: Jeff Mickey <jeff@archlinux.org>
# x86 Maintainer: Thayer Williams <thayer@archlinux.org>
pkgname=dopewars
pkgver=1.5.12
pkgrel=4
pkgdesc="dopewars is a Drug dealing game set in New York"
url="http://dopewars.sourceforge.net/"
license=('GPL')
arch=('i686' 'x86_64' 'ppc')
depends=('gtk2>=2.12.11' 'alsa-lib' 'audiofile' 'esd' 'sdl_mixer' 'ncurses>=5.6-7')
makedepends=('xorg-headers')
install='dopewars.install'
source=("http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz")
md5sums=('debf749de9053dc2fb2e74c37ae06206')

build() {
    cd $srcdir/$pkgname-$pkgver

    ./configure --prefix=/usr --localstatedir=/var/games \
        --mandir=/usr/share/man || return 1
    make || return 1
    make DESTDIR=$pkgdir install || return 1

    # setup shortcut menu entries
    mkdir -p $pkgdir/usr/share/applications
    mv $pkgdir/usr/share/gnome/apps/Games/dopewars.desktop \
       $pkgdir/usr/share/applications
    rm -r $pkgdir/usr/share/gnome

    # set appropriate permissions and destinations
    chown root:games $pkgdir/usr/bin/dopewars
    chmod 2755 $pkgdir/usr/bin/dopewars
    chown root:games $pkgdir/var/games
    chmod 775 $pkgdir/var/games

    # Handle the scores file creation in the install script
    rm $pkgdir/var/games/dopewars.sco
}

