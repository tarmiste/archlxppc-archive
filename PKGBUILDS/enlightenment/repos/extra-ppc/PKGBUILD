# $Id: PKGBUILD 140259 2011-10-11 03:24:04Z eric $
# x86 Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=enlightenment
pkgver=1.0.10
_themever=1.0.1
pkgrel=1
pkgdesc="A fast, flexible, and very extensible Window Manager"
arch=('ppc' 'i686' 'x86_64')
url="http://www.enlightenment.org"
license=('BSD' 'custom')
depends=('libxinerama' 'imlib2' 'perl' 'pango' 'libxcomposite' \
         'libxdamage' 'libxrandr' 'libpulse' 'audiofile')
options=('!libtool')
source=(http://downloads.sourceforge.net/sourceforge/${pkgname}/e16-${pkgver}.tar.gz \
        http://downloads.sourceforge.net/enlightenment/e16-themes-${_themever}.tar.gz)
sha1sums=('20e56c851aeb5279479eb43570b5d58bcc1d6ef9'
          'ae34e2df1294198795736bf953bf695aba93b710')

build() {
  cd "${srcdir}/e16-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --enable-sound-pulse 
  make

  cd "${srcdir}/e16-themes-${_themever}"
  ./configure --prefix=/usr
}

package() {
  cd "${srcdir}/e16-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -D -m755 "${pkgdir}/usr/share/e16/misc/Xclients.e16-gnome.sh" "${pkgdir}/usr/bin/e16-gnome"
  install -D -m755 "${pkgdir}/usr/share/e16/misc/Xclients.e16-kde.sh" "${pkgdir}/usr/bin/e16-kde"
  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
  install -D -m644 fonts/COPYRIGHT.Vera "${pkgdir}/usr/share/licenses/${pkgname}/COPYRIGHT.Vera"

  cd "${srcdir}/e16-themes-${_themever}"
  make DESTDIR="${pkgdir}" install
  chown -R root:root "${pkgdir}"/usr/share/*
}
