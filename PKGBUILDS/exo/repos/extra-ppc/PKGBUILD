# $Id: PKGBUILD 141087 2011-10-23 16:12:32Z andyrtr $
# x86 Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: tobias <tobias funnychar archlinux.org>
# Contributor: Aurelien Foret <orelien@chez.com>

pkgname=exo
pkgver=0.6.2
pkgrel=3
pkgdesc="Extensions to Xfce by os-cillation"
arch=('ppc' 'i686' 'x86_64')
license=('GPL2' 'LGPL')
url="http://www.xfce.org/projects/exo"
groups=('xfce4')
depends=("libxfce4util>=4.8.1" 'gtk2' 'hicolor-icon-theme' )
makedepends=('pygtk' 'perl-uri' 'intltool' 'gtk-doc' 'xorg-iceauth')
optdepends=('perl-uri: for mail-compose helper script')
options=('!libtool')
install=${pkgname}.install
source=(http://archive.xfce.org/src/xfce/${pkgname}/0.6/${pkgname}-${pkgver}.tar.bz2)
md5sums=('e25333df350abc30999cd16fe96ab690')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  # python2 fix
  export PYTHON=python2
  ./configure --prefix=/usr \
	--sysconfdir=/etc \
	--libexecdir=/usr/lib/xfce4 \
	--localstatedir=/var  \
	--disable-static \
	--enable-python \
	--enable-gtk-doc \
	--disable-debug
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
