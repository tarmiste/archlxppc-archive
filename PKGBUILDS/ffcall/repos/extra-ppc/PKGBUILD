# $Id: PKGBUILD 128452 2011-06-24 01:23:15Z eric $
# x86 Maintainer: Vesa Kaihlavirta <vegai@iki.fi>
# Contributor: Michael Baehr <usemike@spamblocked.com>

pkgname=ffcall
pkgver=1.10
pkgrel=4
pkgdesc="C library for implementing foreign function calls in embedded interpreters"
arch=('ppc' 'i686' 'x86_64')
url="http://www.gnu.org/software/libffcall/"
license=('GPL2')
options=('!libtool' '!makeflags')
source=(http://www.haible.de/bruno/gnu/${pkgname}-${pkgver}.tar.gz)
md5sums=('2db95007e901f3bc2ae7e5a9fe9ebea4')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  if [ "$CARCH" = "x86_64" -o "$CARCH" = "ppc" ]; then
      CONFIGFLAG="--with-pic"
  fi
  ./configure --prefix=/usr --mandir=/usr/share/man $CONFIGFLAG
  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  install -d "${pkgdir}"/usr/share/{man,doc/ffcall}
  make DESTDIR="${pkgdir}" htmldir=/usr/share/doc/ffcall install
}
