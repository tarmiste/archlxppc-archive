# $Id: PKGBUILD 142107 2011-11-05 10:13:34Z tpowa $
# x86 Maintainer:

pkgname=findutils
pkgver=4.4.2
pkgrel=4
pkgdesc="GNU utilities to locate files"
arch=('ppc' 'i686' 'x86_64')
license=('GPL3')
groups=('base')
depends=('glibc' 'sh')
url="http://www.gnu.org/software/findutils"
source=(ftp://ftp.gnu.org/pub/gnu/findutils/${pkgname}-${pkgver}.tar.gz)
install=findutils.install
sha1sums=('e8dd88fa2cc58abffd0bfc1eddab9020231bb024')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # Don't build or install locate because we use mlocate,
  # which is a secure version of locate.
  sed -i '/^SUBDIRS/s/locate//' Makefile.in

  ./configure --prefix=/usr
  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR=$pkgdir install
}
