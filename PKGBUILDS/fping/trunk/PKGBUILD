# $Id: PKGBUILD 146192 2012-01-05 20:11:27Z giovanni $
# x86 Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=fping
pkgver=3.0
pkgrel=1
pkgdesc="A utility to ping multiple hosts at once"
arch=('ppc' 'i686' 'x86_64')
url="http://www.fping.org/"
license=('custom') 
depends=('glibc')
source=("http://www.fping.org/dist/${pkgname}-${pkgver}.tar.gz")
md5sums=('e937c9a2b1d65b6f5294fec57891645b')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr \
              --mandir=/usr/share/man
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="${pkgdir}" install

  # install license
  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/$pkgname/LICENSE"
  chmod 4555 ${pkgdir}/usr/sbin/fping
}
