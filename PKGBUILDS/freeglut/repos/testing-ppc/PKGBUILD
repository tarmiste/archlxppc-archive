# $Id$
# x86 Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=freeglut
pkgver=2.8.0
pkgrel=1
pkgdesc="Provides functionality for small OpenGL programs"
arch=('i686' 'x86_64' 'ppc')
url="http://freeglut.sourceforge.net/"
license=('MIT')
depends=('libxxf86vm' 'mesa' 'libxi')
replaces=('glut')
provides=('glut')
conflicts=('glut')
options=('!libtool')
source=(http://downloads.sourceforge.net/freeglut/${pkgname}-${pkgver}.tar.gz)
md5sums=('5db8651af306bc403fbfd36934a20e1d')
sha1sums=('4debbe559c6c9841ce1abaddc9d461d17c6083b1')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --disable-static
  make all
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
