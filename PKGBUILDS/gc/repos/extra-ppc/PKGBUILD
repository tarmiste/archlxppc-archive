# $Id$
# x86 Maintainer: Daniel Isenmann <daniel [at] archlinux.org>
# Contributor: dorphell <dorphell@gmx.net>

pkgname=gc
pkgver=7.1
pkgrel=4
pkgdesc="A garbage collector for C and C++"
arch=('i686' 'x86_64' 'ppc')
url="http://www.hpl.hp.com/personal/Hans_Boehm/gc/"
license=('GPL')
source=("http://www.hpl.hp.com/personal/Hans_Boehm/gc/gc_source/${pkgname}-${pkgver}.tar.gz")
depends=('gcc-libs')
options=('!libtool')
md5sums=('2ff9924c7249ef7f736ecfe6f08f3f9b')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --enable-cplusplus
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}/usr/share/gc"
  install -m755 -d "${pkgdir}/usr/share/doc/gc"
  install -m644 doc/README{,.{changes,contributors,environment,linux}} \
    "${pkgdir}/usr/share/doc/gc/"

  sed -i -e 's/GC_MALLOC 1L/gc 3/' doc/gc.man
  install -m755 -d "${pkgdir}/usr/share/man/man3"
  install -m644 doc/gc.man "${pkgdir}/usr/share/man/man3/gc.3"
}
