# $Id: PKGBUILD 139878 2011-10-05 19:06:35Z andrea $
# x86 Maintainer: Eric BÃ©langer <eric@archlinux.org>
# Contributor: Lukas Jirkovsky <l.jirkovsky@gmail.com>

pkgname=geeqie
pkgver=1.0
pkgrel=6
pkgdesc="A lightweight image browser and viewer (fork of GQview)"
arch=('ppc' 'i686' 'x86_64')
url="http://geeqie.sourceforge.net/"
license=('GPL3')
depends=('exiv2' 'gtk2' 'lcms' 'desktop-file-utils')
makedepends=('intltool' 'doxygen' 'gnome-doc-utils')
optdepends=('librsvg: SVG rendering')
replaces=('gqview-devel')
install=geeqie.install
source=(http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz)
md5sums=('1d67ef990390224c5052697d93bb49c0')
sha1sums=('8494a557d67d20e6ad720645ec789dd2b33a3266')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  CPPFLAGS="-D_FILE_OFFSET_BITS=64" ./configure --prefix=/usr
  make
}

package(){
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
