# $Id: PKGBUILD 146616 2012-01-14 12:13:58Z ibiru $
# x86 Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=gjs
pkgver=1.30.1
pkgrel=1
pkgdesc="Javascript Bindings for GNOME"
arch=('ppc' 'i686' 'x86_64')
url="http://live.gnome.org/Gjs"
license=('GPL')
depends=('cairo' 'dbus-glib' 'gobject-introspection' 'js')
options=('!libtool')
source=(http://download.gnome.org/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
sha256sums=('f5db07ddf70458a33a5d0bdf83f84070fc234237ecb0d49a8676e67b52119a05')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -i 's|python|python2|' scripts/make-tests
  ./configure --prefix=/usr --disable-static
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
