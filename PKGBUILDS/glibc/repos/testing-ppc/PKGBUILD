# $Id$
# x86 Maintainer: Allan McRae <allan@archlinux.org>

# toolchain build order: linux-api-headers->glibc->binutils->gcc->binutils->glibc
# NOTE: valgrind requires rebuilt with each major glibc version

pkgname=glibc
pkgver=2.15
pkgrel=10
_glibcdate=20111227
pkgdesc="GNU C Library"
arch=('i686' 'x86_64' 'ppc')
url="http://www.gnu.org/software/libc"
license=('GPL' 'LGPL')
groups=('base')
depends=('linux-api-headers>=3.2' 'tzdata')
makedepends=('gcc>=4.6')
backup=(etc/gai.conf
        etc/locale.gen
        etc/nscd.conf)
options=('!strip')
install=glibc.install
source=(http://mirrors.kernel.org/archlinux/other/glibc/${pkgname}-${pkgver}_${_glibcdate}.tar.xz
        glibc-2.15-do-not-install-timezone-files.patch
        glibc-2.15-do-not-install-timezone-files-2.patch
        glibc-__i686.patch
        glibc-2.12.2-ignore-origin-of-privileged-program.patch
        glibc-2.14-libdl-crash.patch
        glibc-2.14-reexport-rpc-interface.patch
        glibc-2.14-reinstall-nis-rpc-headers.patch
        glibc-2.15-fix-res_query-assert.patch
        glibc-2.15-regex.patch
        glibc-2.15-lddebug-scopes.patch
        glibc-2.15-revert-c5a0802a.patch
        glibc-2.15-scanf.patch
        glibc-2.15-ifunc.patch
        glibc-2.15-avx.patch
        glibc-2.15-strcasecmp-disable-avx.patch
        glibc-2.15-gb18030.patch
        glibc-2.15-revert-netlink-cache.patch
        glibc-2.15-arena.patch
        glibc-2.15-negative-result-cache.patch
        glibc-2.15-multiarch-x86-strcmp.patch
        glibc-2.15-vdso.patch
        glibc-2.15-feraiseexcept-plt.patch
        glibc-2.15-vfprintf-nargs.patch
        glibc-2.15-__libc_res_nquerydomain-out-of-bounds.patch
        glibc-2.15-fmtmsg-locking.patch
        glibc-2.15-non-signalling-comparisons.patch
        glibc-2.15-rintf-rounding.patch
        glibc-2.15-nearbyintf-rounding.patch
        glibc-2.15-confstr-local-buffer-extent.patch
        nscd
        locale.gen.txt
        locale-gen)
md5sums=('6ffdf5832192b92f98bdd125317c0dfc'
         '7ef69c530a15106de93e4de2df2d393e'
         'b6c619e5cf91829a15ce34dccef676d5'
         'addfddd648a4bf832eb126aba944ebae'
         'e60e33591c9ec1447e4cddadcbb9cf3a'
         '6970bcfeb3bf88913436d5112d16f588'
         'c5de2a946215d647c8af5432ec4b0da0'
         '55febbb72139ac7b65757df085024b83'
         '31f415b41197d85d3bbee3d1eecd06a3'
         'b3526cbd5e29773560dba725db99af5a'
         '3c219ddfb619b6df903cac4cc42c611d'
         '7ae3e426251ae33e73dbad71f9c91378'
         'f0782ddbf38e0b30ec6b85348816046f'
         '3d844b53b2dbb7c996e39c7ad932f55d'
         '41ae047ac88e8f6f547c70b0a0bc3b72'
         'fccb89f6628f59752278e125c35941f8'
         'c4cd34f20ccd37817f6c1374bd4ee68e'
         '94b61302a7ca6c5764d013dc7738fcfe'
         'a9ffadcfd2d357f91fee0b861fd4a7c6'
         '2c46b8e294de24c531f2253ff69aeef3'
         '7a2998a04ebfcf8bf820540f490ce714'
         '0d77d20fa7fe2f87ad945cb9edb4d91d'
         'bfdefac3d705f41fbf84b1de1dc945af'
         '3443e89c1e98089cd6c3e3c23f0c3d85'
         '340deaa582a95ddde86edb624c3bfea0'
         '6bbac50e6ff82187654e6a0a7bd849e7'
         'c483504cf404ed0b44480af627813a97'
         '1419d61fd1dbc6cdc48bb59da86fa66f'
         '7ff501435078b1a2622124fbeaafc921'
         '8d1023a51e0932681b46440d5f8551ee'
         'b587ee3a70c9b3713099295609afde49'
         '07ac979b6ab5eeb778d55f041529d623'
         '476e9113489f93b348b21e144b6a8fcf')


mksource() {
  git clone git://sourceware.org/git/glibc.git
  pushd glibc
  #git checkout -b glibc-2.15-arch origin/release/2.15/master
  git checkout -b glibc-2.15-arch origin/master
  popd
  tar -cvJf glibc-${pkgver}_${_glibcdate}.tar.xz glibc/*
}


build() {
  cd ${srcdir}/glibc

  # timezone data is in separate package (tzdata)
  # http://sourceware.org/git/?p=glibc.git;a=commit;h=482ff4da
  patch -p1 -i ${srcdir}/glibc-2.15-do-not-install-timezone-files.patch
  # http://sourceware.org/git/?p=glibc.git;a=commit;h=a458e7fe
  patch -p1 -i ${srcdir}/glibc-2.15-do-not-install-timezone-files-2.patch

  # undefine __i686
  # http://sourceware.org/glibc/wiki/Release/2.15#Build_Failures
  patch -p1 -i ${srcdir}/glibc-__i686.patch

  # http://www.exploit-db.com/exploits/15274/
  # http://sourceware.org/git/?p=glibc.git;a=patch;h=d14e6b09  (fedora branch)
  patch -p1 -i ${srcdir}/glibc-2.12.2-ignore-origin-of-privileged-program.patch

  # http://sourceware.org/git/?p=glibc.git;a=commitdiff;h=675155e9  (fedora branch)
  # http://sourceware.org/ml/libc-alpha/2011-06/msg00006.html
  patch -p1 -i ${srcdir}/glibc-2.14-libdl-crash.patch

  # re-export RPC interface until libtirpc is ready as a replacement
  # http://sourceware.org/git/?p=glibc.git;a=commitdiff;h=acee4873  (fedora branch)
  patch -p1 -i ${srcdir}/glibc-2.14-reexport-rpc-interface.patch
  # http://sourceware.org/git/?p=glibc.git;a=commitdiff;h=bdd816a3  (fedora branch)
  patch -p1 -i ${srcdir}/glibc-2.14-reinstall-nis-rpc-headers.patch

  # fix res_query assertion
  # http://sourceware.org/bugzilla/show_bug.cgi?id=13013
  patch -p1 -i ${srcdir}/glibc-2.15-fix-res_query-assert.patch

  # fix up regcomp/regexec
  # http://sourceware.org/git/?p=glibc.git;a=commit;h=2ba92745
  patch -p1 -i ${srcdir}/glibc-2.15-regex.patch

  # propriety nvidia crash - https://bugzilla.redhat.com/show_bug.cgi?id=737223 
  # http://sourceware.org/git/?p=glibc.git;a=commitdiff;h=0c95ab64  (fedora branch)
  patch -p1 -i ${srcdir}/glibc-2.15-lddebug-scopes.patch

  # revert commit c5a0802a - causes various hangs
  # https://bugzilla.redhat.com/show_bug.cgi?id=769421
  # Note: fedora may have actual fix (not submitted upstream yet...)
  # http://pkgs.fedoraproject.org/gitweb/?p=glibc.git;a=blob_plain;f=glibc-rh552960-2.patch
  patch -p1 -i ${srcdir}/glibc-2.15-revert-c5a0802a.patch

  # fix realloc usage in vfscanf
  # http://sourceware.org/git/?p=glibc.git;a=commit;h=20b38e03
  patch -p1 -i ${srcdir}/glibc-2.15-scanf.patch

  # fix ifunc relocations
  # http://sourceware.org/git/?p=glibc.git;a=commit;h=6ee65ed6
  patch -p1 -i ${srcdir}/glibc-2.15-ifunc.patch
        
  # fix AVX detection
  # http://sourceware.org/git/?p=glibc.git;a=commit;h=afc5ed09
  # http://sourceware.org/git/?p=glibc.git;a=commit;h=08cf777f
  patch -p1 -i ${srcdir}/glibc-2.15-avx.patch
  # and "fix" strcasecmp
  patch -p1 -i ${srcdir}/glibc-2.15-strcasecmp-disable-avx.patch

  # fix GB18030 charmap
  # http://sourceware.org/bugzilla/show_bug.cgi?id=11837
  # http://sourceware.org/git/?p=glibc.git;a=commit;h=2a57bd79  (fedora branch)
  # http://sourceware.org/git/?p=glibc.git;a=commit;h=3d828a61  (fedora branch)
  patch -p1 -i ${srcdir}/glibc-2.15-gb18030.patch

  # fix crash in __nscd_get_mapping if nscd not running
  # http://sourceware.org/bugzilla/show_bug.cgi?id=13594 (potential "fix" in comment)
  # reverts commit 3a2c0242 and other necessary following changes...
  patch -p1 -i ${srcdir}/glibc-2.15-revert-netlink-cache.patch

  # handle ARENA_TEST correctly
  # http://sourceware.org/git/?p=glibc.git;a=commit;h=41b81892
  patch -p1 -i ${srcdir}/glibc-2.15-arena.patch

  # Do not cache negative results in nscd if these are transient
  # http://sourceware.org/git/?p=glibc.git;a=commit;h=3e1aa84e
  patch -p1 -i ${srcdir}/glibc-2.15-negative-result-cache.patch

  # strcasecmp_l, strncasecmp_l act as strcmp for multiarch x86
  # http://sourceware.org/git/?p=glibc.git;a=commit;h=0bab47b6
  patch -p1 -i ${srcdir}/glibc-2.15-multiarch-x86-strcmp.patch
  
  # always set l_used for vDSO.
  # http://sourceware.org/git/?p=glibc.git;a=commit;h=1f393a11
  patch -p1 -i ${srcdir}/glibc-2.15-vdso.patch

  # fix x86 PLT slot usage for feraiseexcept
  # http://sourceware.org/git/?p=glibc.git;a=commit;h=7c35ffed
  patch -p1 -i ${srcdir}/glibc-2.15-feraiseexcept-plt.patch

  # vfprintf nargs overflow - CVE-2012-0864
  # http://sourceware.org/git/?p=glibc.git;a=commit;h=7c1f4834
  patch -p1 -i ${srcdir}/glibc-2.15-vfprintf-nargs.patch

  # avoid out ouf bounds read in __libc_res_nquerydomain
  # http://sourceware.org/git/?p=glibc.git;a=commit;h=8fdceb2e
  patch -p1 -i ${srcdir}/glibc-2.15-__libc_res_nquerydomain-out-of-bounds.patch

  # make fmtmsg function thread-safe
  # http://sourceware.org/git/?p=glibc.git;a=commit;h=7724defc
  patch -p1 -i ${srcdir}/glibc-2.15-fmtmsg-locking.patch

  # use non-signaling floating-point comparisons in math functions
  # http://sourceware.org/git/?p=glibc.git;a=commit;h=92221550
  patch -p1 -i ${srcdir}/glibc-2.15-non-signalling-comparisons.patch

  # fix rintf rounding.
  # http://sourceware.org/git/?p=glibc.git;a=commit;h=fe45ce09
  patch -p1 -i ${srcdir}/glibc-2.15-rintf-rounding.patch

  # fix nearbyintf rounding
  # http://sourceware.org/git/?p=glibc.git;a=commit;h=6cbeae47
  patch -p1 -i ${srcdir}/glibc-2.15-nearbyintf-rounding.patch

  # fix varaible scope issue in confstr
  # http://sourceware.org/git/?p=glibc.git;a=commit;h=ac4c54f0
  # http://sourceware.org/git/?p=glibc.git;a=commit;h=d6a403f9
  patch -p1 -i ${srcdir}/glibc-2.15-confstr-local-buffer-extent.patch

  install -dm755 ${pkgdir}/etc
  touch ${pkgdir}/etc/ld.so.conf

  cd ${srcdir}
  mkdir glibc-build
  cd glibc-build

  if [[ ${CARCH} = "i686" ]]; then
    # Hack to fix NPTL issues with Xen, only required on 32bit platforms
    # TODO: make separate glibc-xen package for i686
    export CFLAGS="${CFLAGS} -mno-tls-direct-seg-refs"
  fi

  echo "slibdir=/lib" >> configparms

  # remove hardening options from CFLAGS for building libraries
  CFLAGS=${CFLAGS/-fstack-protector/}
  CFLAGS=${CFLAGS/-D_FORTIFY_SOURCE=2/}

  ${srcdir}/glibc/configure --prefix=/usr \
      --libdir=/usr/lib --libexecdir=/usr/lib \
      --with-headers=/usr/include \
      --enable-add-ons=nptl,libidn \
      --enable-kernel=2.6.32 \
      --with-tls --with-__thread \
      --enable-bind-now --without-gd \
      --without-cvs --disable-profile \
      --enable-multi-arch

  # build libraries with hardening disabled
  echo "build-programs=no" >> configparms
  make
  
  # re-enable hardening for programs
  sed -i "s#=no#=yes#" configparms
  echo "CC += -fstack-protector -D_FORTIFY_SOURCE=2" >> configparms
  echo "CXX += -fstack-protector -D_FORTIFY_SOURCE=2" >> configparms
  make

  # remove harding in preparation to run test-suite
  sed -i '2,4d' configparms
}

check() {
  cd ${srcdir}/glibc-build

  # some errors are expected - manually check log files
  make -k check || true
}

package() {
  cd ${srcdir}/glibc-build
  make install_root=${pkgdir} install

  rm -f ${pkgdir}/etc/ld.so.{cache,conf}

  install -dm755 ${pkgdir}/etc/rc.d
  install -dm755 ${pkgdir}/usr/sbin
  install -dm755 ${pkgdir}/usr/lib/locale
  install -m644 ${srcdir}/glibc/nscd/nscd.conf ${pkgdir}/etc/nscd.conf
  install -m755 ${srcdir}/nscd ${pkgdir}/etc/rc.d/nscd
  install -m755 ${srcdir}/locale-gen ${pkgdir}/usr/sbin
  install -m644 ${srcdir}/glibc/posix/gai.conf ${pkgdir}/etc/gai.conf

  sed -i -e 's/^\tserver-user/#\tserver-user/' ${pkgdir}/etc/nscd.conf

  # create /etc/locale.gen
  install -m644 ${srcdir}/locale.gen.txt ${pkgdir}/etc/locale.gen
  sed -i "s|/| |g" ${srcdir}/glibc/localedata/SUPPORTED
  sed -i 's|\\| |g' ${srcdir}/glibc/localedata/SUPPORTED
  sed -i "s|SUPPORTED-LOCALES=||" ${srcdir}/glibc/localedata/SUPPORTED
  cat ${srcdir}/glibc/localedata/SUPPORTED >> ${pkgdir}/etc/locale.gen
  sed -i "s|^|#|g" ${pkgdir}/etc/locale.gen

  if [[ ${CARCH} = "x86_64" ]]; then
    # fix for the linker
    sed -i '/RTLDLIST/s%lib64%lib%' ${pkgdir}/usr/bin/ldd
    # Comply with multilib binaries, they look for the linker in /lib64
    mkdir ${pkgdir}/lib64
    cd ${pkgdir}/lib64
    ln -v -s ../lib/ld* .
  fi
  
  # Do not strip the following files for improved debugging support
  # ("improved" as in not breaking gdb and valgrind...):
  #   ld-${pkgver}.so
  #   libc-${pkgver}.so
  #   libpthread-${pkgver}.so
  #   libthread_db-1.0.so

  cd $pkgdir
  strip $STRIP_BINARIES sbin/{ldconfig,sln} \
                        usr/bin/{gencat,getconf,getent,iconv,locale,localedef} \
                        usr/bin/{makedb,pcprofiledump,pldd,rpcgen,sprof} \
                        usr/lib/getconf/* \
                        usr/sbin/{iconvconfig,nscd}
  [[ $CARCH = "i686" ]] && strip $STRIP_BINARIES usr/bin/lddlibc4

  strip $STRIP_STATIC usr/lib/*.a

  strip $STRIP_SHARED lib/{libanl,libBrokenLocale,libcidn,libcrypt}-${pkgver}.so \
                      lib/libnss_{compat,db,dns,files,hesiod,nis,nisplus}-${pkgver}.so \
                      lib/{libdl,libm,libnsl,libresolv,librt,libutil}-${pkgver}.so \
                      lib/{libmemusage,libpcprofile,libSegFault}.so \
                      usr/lib/{pt_chown,{audit,gconv}/*.so}
}
