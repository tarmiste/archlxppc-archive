# $Id: PKGBUILD 119495 2011-04-11 19:26:23Z andrea $
# x86 Maintainer:
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Roman Kyrylych <roman@archlinux.org>
# Contributor: Aurelien Foret <orelien@chez.com>

pkgname=gnokii
pkgver=0.6.30
pkgrel=2
pkgdesc="Tools and user space driver for use with mobile phones"
arch=('ppc' 'i686' 'x86_64')
url="http://www.gnokii.org/"
license=('GPL')
depends=('libusb-compat' 'libxpm' 'bluez' 'libical')
makedepends=('gtk2' 'libmysqlclient' 'postgresql-libs' 'intltool')
optdepends=('gtk2: xgnokii GUI'
            'libmysqlclient: smsd mysql backend'
	        'postgresql-libs: smsd postgresql backend')
backup=('etc/gnokiirc')
options=('!libtool' '!makeflags')
source=("http://www.gnokii.org/download/$pkgname/$pkgname-${pkgver}.tar.bz2"
	    'gnokii.patch')
md5sums=('c90137d403febbc16712d64f0eb196de'
         '4d764727686ee34bc73489c8b1321c82')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # FS#13249
  sed -i 's|cellphone|phone|' xgnokii/xgnokii.desktop.in

  patch -Np1 -i "${srcdir}/gnokii.patch"

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --enable-security \
    --disable-unix98test
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -Dm644 Docs/sample/gnokiirc "${pkgdir}/etc/gnokiirc"
}
