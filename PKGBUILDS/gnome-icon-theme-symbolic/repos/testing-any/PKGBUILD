# $Id$
# Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=gnome-icon-theme-symbolic
pkgver=3.4.0
pkgrel=1
pkgdesc="GNOME icon theme, symbolic icons"
arch=(any)
depends=('gtk-update-icon-cache' 'icon-naming-utils' 'hicolor-icon-theme')
makedepends=('intltool')
url="http://www.gnome.org"
license=('GPL')
install=gnome-icon-theme-symbolic.install
groups=('gnome')
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver:0:3}/$pkgname-$pkgver.tar.xz)
sha256sums=('8f2738b1e8992827427f007e0eafc8deed32039c2bcf8345b82243864292d021')

build() {
  cd "$pkgname-$pkgver"
  GTK_UPDATE_ICON_CACHE=/bin/true ./configure --prefix=/usr
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
