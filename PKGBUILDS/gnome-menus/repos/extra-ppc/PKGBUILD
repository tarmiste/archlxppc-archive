# $Id: PKGBUILD 139302 2011-10-01 18:57:56Z ibiru $
# x86 Maintainer: Jan de Groot <jan@archlinux.org>

pkgname=gnome-menus
pkgver=3.2.0.1
pkgrel=1
pkgdesc="GNOME menu specifications"
arch=('ppc' 'i686' 'x86_64')
depends=('glib2' 'python2')
makedepends=('intltool' 'gobject-introspection')
options=('!libtool')
license=('GPL' 'LGPL')
url="http://www.gnome.org"
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/3.2/${pkgname}-${pkgver}.tar.xz
        menus.patch)
sha256sums=('17e2ea745b1e80fba4f6ec03c4bac561aa6e7ca275fcc79a3df3d318cb64972b'
            'df2779777906b038fa911dc745534564ede9524a64ab368e2f6f8e38d54e7acc')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  patch -Np0 -i "$srcdir/menus.patch"

  PYTHON=/usr/bin/python2 ./configure \
      --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var  --disable-static
  make
}

package(){
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  sed -i "1s|#!/usr/bin/env python$|&2|" \
    "$pkgdir/usr/lib/python2.7/site-packages/GMenuSimpleEditor/config.py"
}
