# $Id$
# Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>

pkgbase=gnome-shell-extensions
pkgver=3.2.1
pkgrel=1
arch=(any)
url="http://live.gnome.org/GnomeShell/Extensions"
license=(GPL2)
makedepends=(gnome-shell intltool)
groups=(gnome-shell-extensions)
#source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgbase/${pkgver%.*}/$pkgbase-$pkgver.tar.xz)
source=(ftp://ftp.archlinux.org/other/$pkgbase/$pkgbase-$pkgver-$pkgrel.tar.xz)
sha256sums=('5ac1a7f10ef78a3984578428ecd1610fb1c311a1cb66be8aad5d70bbba417eb0')

pkgname=(gnome-shell-extensions-common
         gnome-shell-extension-alternate-tab
         gnome-shell-extension-windows-navigator
         gnome-shell-extension-dock
         gnome-shell-extension-auto-move-windows
         gnome-shell-extension-user-theme
         gnome-shell-extension-drive-menu
         gnome-shell-extension-alternative-status-menu
         gnome-shell-extension-places-menu
         gnome-shell-extension-native-window-placement
         gnome-shell-extension-system-monitor
         gnome-shell-extension-apps-menu
         gnome-shell-extension-workspace-indicator
         gnome-shell-extension-xrandr-indicator
         gnome-shell-extension-gajim)

build() {
  _extensions=(alternate-tab windowsNavigator dock auto-move-windows user-theme
               drive-menu alternative-status-menu places-menu
               native-window-placement systemMonitor apps-menu
               workspace-indicator xrandr-indicator gajim)

  cd "$srcdir/$pkgbase-$pkgver"
  ./configure --prefix=/usr \
    --enable-extensions="${_extensions[*]}" \
    --disable-schemas-compile
  make
}

package_gnome-shell-extensions-common() {
  pkgdesc="Extensions for GNOME Shell - common files"
  depends=(gnome-shell)
  groups=()

  cd "$srcdir/$pkgbase-$pkgver/po"
  make DESTDIR="$pkgdir" install
}

package_gnome-shell-extension-alternate-tab() {
  pkgdesc="Alternate Tab extension for GNOME Shell"
  depends=(gnome-shell-extensions-common)
  install=gnome-shell-extensions.install

  cd "$srcdir/$pkgbase-$pkgver/extensions/alternate-tab"
  make DESTDIR="$pkgdir" install
}

package_gnome-shell-extension-windows-navigator() {
  pkgdesc="Windows Navigator extension for GNOME Shell"
  depends=(gnome-shell-extensions-common)

  cd "$srcdir/$pkgbase-$pkgver/extensions/windowsNavigator"
  make DESTDIR="$pkgdir" install
}

package_gnome-shell-extension-dock() {
  pkgdesc="Dock extension for GNOME Shell"
  depends=(gnome-shell-extensions-common)
  install=gnome-shell-extensions.install

  cd "$srcdir/$pkgbase-$pkgver/extensions/dock"
  make DESTDIR="$pkgdir" install
}

package_gnome-shell-extension-auto-move-windows() {
  pkgdesc="Auto Move Windows extension for GNOME Shell"
  depends=(gnome-shell-extensions-common)
  install=gnome-shell-extensions.install

  cd "$srcdir/$pkgbase-$pkgver/extensions/auto-move-windows"
  make DESTDIR="$pkgdir" install
}

package_gnome-shell-extension-user-theme() {
  pkgdesc="User Theme extension for GNOME Shell"
  depends=(gnome-shell-extensions-common)
  install=gnome-shell-extensions.install

  cd "$srcdir/$pkgbase-$pkgver/extensions/user-theme"
  make DESTDIR="$pkgdir" install
}

package_gnome-shell-extension-drive-menu() {
  pkgdesc="Drive Menu extension for GNOME Shell"
  depends=(gnome-shell-extensions-common)

  cd "$srcdir/$pkgbase-$pkgver/extensions/drive-menu"
  make DESTDIR="$pkgdir" install
}

package_gnome-shell-extension-alternative-status-menu() {
  pkgdesc="Alternative Status Menu extension for GNOME Shell"
  depends=(gnome-shell-extensions-common)

  cd "$srcdir/$pkgbase-$pkgver/extensions/alternative-status-menu"
  make DESTDIR="$pkgdir" install
}

package_gnome-shell-extension-places-menu() {
  pkgdesc="Places Menu extension for GNOME Shell"
  depends=(gnome-shell-extensions-common)

  cd "$srcdir/$pkgbase-$pkgver/extensions/places-menu"
  make DESTDIR="$pkgdir" install
}

package_gnome-shell-extension-native-window-placement() {
  pkgdesc="Native Window Placement extension for GNOME Shell"
  depends=(gnome-shell-extensions-common)
  install=gnome-shell-extensions.install

  cd "$srcdir/$pkgbase-$pkgver/extensions/native-window-placement"
  make DESTDIR="$pkgdir" install
}

package_gnome-shell-extension-system-monitor() {
  pkgdesc="System Monitor extension for GNOME Shell"
  depends=(gnome-shell-extensions-common)

  cd "$srcdir/$pkgbase-$pkgver/extensions/systemMonitor"
  make DESTDIR="$pkgdir" install
}

package_gnome-shell-extension-apps-menu() {
  pkgdesc="Apps Menu extension for GNOME Shell"
  depends=(gnome-shell-extensions-common)

  cd "$srcdir/$pkgbase-$pkgver/extensions/apps-menu"
  make DESTDIR="$pkgdir" install
}

package_gnome-shell-extension-workspace-indicator() {
  pkgdesc="Workspace Indicator extension for GNOME Shell"
  depends=(gnome-shell-extensions-common)

  cd "$srcdir/$pkgbase-$pkgver/extensions/workspace-indicator"
  make DESTDIR="$pkgdir" install
}

package_gnome-shell-extension-xrandr-indicator() {
  pkgdesc="XRandR Indicator extension for GNOME Shell"
  depends=(gnome-shell-extensions-common)

  cd "$srcdir/$pkgbase-$pkgver/extensions/xrandr-indicator"
  make DESTDIR="$pkgdir" install
}

package_gnome-shell-extension-gajim() {
  pkgdesc="Gajim extension for GNOME Shell"
  depends=(gnome-shell-extensions-common)

  cd "$srcdir/$pkgbase-$pkgver/extensions/gajim"
  make DESTDIR="$pkgdir" install
}
