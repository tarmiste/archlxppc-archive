# $Id: PKGBUILD 143698 2011-11-28 13:17:55Z giovanni $
# x86 Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Jake VanderKolk <jakevanderkolk@gmail.com>

pkgname=gtk-qt-engine
pkgver=1.1
pkgrel=3
pkgdesc="A GTK engine which makes your GTK2 Apps match your QT theme"
arch=('ppc' 'i686' 'x86_64')
license=('GPL')
url="http://code.google.com/p/gtk-qt-engine/"
depends=('libbonoboui' 'kdelibs')
makedepends=('cmake' 'automoc4' 'pkgconfig')
source=("http://gtk-qt-engine.googlecode.com/files/${pkgname}-${pkgver}.tar.bz2")
md5sums=('de8048baef7dfe6c97cd97c463d66152')

build() {
  cd "${srcdir}/${pkgname}"

  cmake . -DCMAKE_BUILD_TYPE=Release \
          -DCMAKE_SKIP_RPATH=ON \
          -DCMAKE_{SHARED,MODULE,EXE}_LINKER_FLAGS='-Wl,--no-undefined -Wl,--as-needed' \
          -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}"

  make DESTDIR="${pkgdir}" install
}
