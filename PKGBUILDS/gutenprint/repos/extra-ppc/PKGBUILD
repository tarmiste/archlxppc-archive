# $Id: PKGBUILD 143830 2011-11-29 16:30:26Z andyrtr $
# x86 Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgname=gutenprint
pkgver=5.2.7
pkgrel=3
pkgdesc="Top quality printer drivers for POSIX systems"
arch=('ppc' 'i686' 'x86_64')
license=('GPL')
install=gutenprint.install
depends=('readline' 'gnutls>=2.12.3') # needs to be checked. build log says -Lgnutls but namcap doesn't detect it
makedepends=('gimp>=2.6.11' 'gtk2>=2.24.4' 'cups>=1.4.6' 'foomatic-db-engine' 'ghostscript>=9.02')
optdepends=('cups:		to use cups printer spooler(recommended)'
            'foomatic-db-engine:	to use foomatic spooler'
            'ghostscript:	adds postscript support for ijsgutenprint'
            'gimp:		adds gutenprint plugin to gimp')
source=(http://downloads.sourceforge.net/gimp-print/$pkgname-$pkgver.tar.bz2 buildfix.patch)
url="http://gimp-print.sourceforge.net/"
replaces=('gimp-print')
options=('!libtool' '!emptydirs')
md5sums=('b19029972bf28f6efd693270daf8f5de'
         '2354cdecf1fbab32eca84266c8c72e7d')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  patch -Np1 -i ${srcdir}/buildfix.patch
  ./configure --prefix=/usr --disable-samples --with-cups --with-gimp2 \
	--disable-translated-cups-ppds --with-gimp2-as-gutenprint \
	--disable-libgutenprintui  --enable-libgutenprintui2 --disable-gtktest \
	--enable-cups-ppds --enable-cups-ppds-at-top-level \
	--with-foomatic --with-foomatic3 --with-ghostscript \
	--disable-static --disable-static-genppd --with-ijs \
	--disable-globalized-cups-ppds
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
}

