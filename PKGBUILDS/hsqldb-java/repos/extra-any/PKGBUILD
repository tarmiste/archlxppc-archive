# $Id: PKGBUILD 144885 2011-12-08 21:37:17Z andyrtr $
# x86 Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Andrew Wright <andreww@photism.org>

pkgname=hsqldb-java
pkgver=1.8.0.10
pkgrel=2
epoch=1
pkgdesc="HSQLDB Java libraries"
arch=('any')
license=('custom')
makedepends=('openjdk6' 'unzip' 'apache-ant')
source=(http://downloads.sourceforge.net/sourceforge/hsqldb/hsqldb_${pkgver//./_}.zip)
url="http://hsqldb.org/"
md5sums=('17410483b5b5f267aa18b7e00b65e6e0')

build() {
  [ -z "${JAVA_HOME}" ] && . /etc/profile.d/openjdk6.sh
  [ -z "${ANT_HOME}" ] && . /etc/profile.d/apache-ant.sh

  cd ${srcdir}/hsqldb/build

  ant jar
}

package() {
  cd ${srcdir}/hsqldb/build

  install -m755 -d ${pkgdir}/usr/share/java
  install -m644 ${srcdir}/hsqldb/lib/hsqldb.jar ${pkgdir}/usr/share/java/

  install -m755 -d ${pkgdir}/usr/share/licenses/${pkgname}
  install -m644 ${srcdir}/hsqldb/doc/hsqldb_lic.txt ${pkgdir}/usr/share/licenses/${pkgname}/
}
