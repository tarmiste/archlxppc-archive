# $Id: PKGBUILD 147157 2012-01-23 15:27:36Z ibiru $
# x86 Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Judd Vinet <jvinet@zeroflux.org>

pkgname=iproute2
pkgver=3.2.0
pkgrel=2
pkgdesc="IP Routing Utilities"
arch=('ppc' 'i686' 'x86_64')
license=('GPL2')
url="http://www.linux-foundation.org/en/Net:Iproute2"
depends=('glibc' 'db')
makedepends=('linux-atm')
optdepends=('linux-atm: ATM support')
provides=('iproute')
conflicts=('iproute')
replaces=('iproute')
options=('!makeflags')
backup=('etc/iproute2/ematch_map' 'etc/iproute2/rt_dsfield' 'etc/iproute2/rt_protos' \
	'etc/iproute2/rt_realms' 'etc/iproute2/rt_scopes' 'etc/iproute2/rt_tables')
source=(http://www.kernel.org/pub/linux/utils/net/$pkgname/$pkgname-$pkgver.tar.xz
	iproute2-fhs.patch
        fix-ip-l.patch)
sha1sums=('1e217f22b0bbfc870ddf746de883ee375cd9e533'
          '2416b11252364d7a6c742eabb4a6924a75637a46'
          '2ec5513c44f89046438d65e2cda1a014010e3b73')

build() {
  cd $srcdir/$pkgname-$pkgver

  # set correct fhs structure
  patch -Np1 -i "$srcdir/iproute2-fhs.patch"

  #upstream fixes
  patch -Np1 -i "$srcdir/fix-ip-l.patch"

  ./configure

  make
}

package() {
  cd $srcdir/$pkgname-$pkgver

  make DESTDIR="$pkgdir" install

  # allow loopback to be started before /usr is mounted, this may not be supported in the future
  mkdir -p "$pkgdir/sbin"
  mv "$pkgdir/usr/sbin/ip" "$pkgdir/sbin/ip"
  ln -s /sbin/ip "$pkgdir/usr/sbin/ip"

  # libnetlink isn't installed, install it FS#19385
  install -Dm644 include/libnetlink.h "$pkgdir/usr/include/libnetlink.h"
  install -Dm644 lib/libnetlink.a "$pkgdir/usr/lib/libnetlink.a"
}
