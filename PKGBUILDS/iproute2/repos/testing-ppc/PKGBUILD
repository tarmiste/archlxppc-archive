# $Id$
# x86 Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Judd Vinet <jvinet@zeroflux.org>

pkgname=iproute2
pkgver=3.3.0
pkgrel=1
pkgdesc="IP Routing Utilities"
arch=('i686' 'x86_64' 'ppc')
license=('GPL2')
url="http://www.linuxfoundation.org/collaborate/workgroups/networking/iproute2"
depends=('glibc' 'db')
makedepends=('linux-atm')
optdepends=('linux-atm: ATM support')
provides=('iproute')
conflicts=('iproute')
replaces=('iproute')
options=('!makeflags')
backup=('etc/iproute2/ematch_map' 'etc/iproute2/rt_dsfield' 'etc/iproute2/rt_protos' \
	'etc/iproute2/rt_realms' 'etc/iproute2/rt_scopes' 'etc/iproute2/rt_tables')
source=(http://www.kernel.org/pub/linux/utils/net/$pkgname/$pkgname-$pkgver.tar.xz
         iproute2-fhs.patch)
sha1sums=('25307c2418b9d4c6433d61a296f50b886da84b8c'
          '2416b11252364d7a6c742eabb4a6924a75637a46')

build() {
  cd $srcdir/$pkgname-$pkgver

  # set correct fhs structure
  patch -Np1 -i "$srcdir/iproute2-fhs.patch"

  ./configure

  make
}

package() {
  cd $srcdir/$pkgname-$pkgver

  make DESTDIR="$pkgdir" install

  # allow loopback to be started before /usr is mounted, this may not be supported in the future
  mkdir -p "$pkgdir/sbin"
  mv "$pkgdir/usr/sbin/ip" "$pkgdir/sbin/ip"
  ln -s /sbin/ip "$pkgdir/usr/sbin/ip"

  # libnetlink isn't installed, install it FS#19385
  install -Dm644 include/libnetlink.h "$pkgdir/usr/include/libnetlink.h"
  install -Dm644 lib/libnetlink.a "$pkgdir/usr/lib/libnetlink.a"
}
