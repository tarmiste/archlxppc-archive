# $Id: PKGBUILD 80408 2010-05-17 07:51:16Z tpowa $
# x86 Maintainer: Tobias Powalowski <tpowa@archlinux.org>
# x86 Maintainer: Thomas Baechler <thomas@archlinux.org>
pkgbase="kernel26-pmac"
pkgname=('kernel26-pmac' 'kernel26-pmac-headers' 'kernel26-manpages' 'kernel26-docs') # Build stock -ARCH kernel
# pkgname=kernel26-custom       # Build kernel with a different name
_kernelname=${pkgname#kernel26}
_basekernel=2.6.39
pkgver=2.6.39.4
pkgrel=1
arch=(ppc)
license=('GPL2')
url="http://www.kernel.org"
makedepends=('xmlto' 'docbook-xsl')
# do not strip binaries, external modules will fail to build
options=('!strip')
source=(http://www.kernel.org/pub/linux/kernel/v2.6/linux-$_basekernel.tar.bz2
	http://www.kernel.org/pub/linux/kernel/v2.6/patch-${pkgver}.bz2
	unionfs-2.5.9.2_for_2.6.39.2.diff
        logo_linux_clut224.ppm
        logo_linux_mono.pbm
        logo_linux_vga16.ppm
        # the main kernel config files
        config
        # standard config files for mkinitcpio ramdisk
        ${pkgname}.preset)

build() {
  unset LDFLAGS

  # Just vanilla
  cd ${srcdir}/linux-$_basekernel

  # maintenence patch
  patch -Np1 -i ${srcdir}/patch-${pkgver}

  # unionfs for the installer
  patch -Np1 -i ${srcdir}/unionfs-2.5.9.2_for_2.6.39.2.diff

  cp ${startdir}/config .config
  sed -i "s|CONFIG_LOCALVERSION=.*|CONFIG_LOCALVERSION=\"-ARCH\"|g" ./.config

  # remove the extraversion from Makefile
  sed -i 's|^EXTRAVERSION = .*$|EXTRAVERSION =|g' Makefile  

  # Arch logo!
  cp ${srcdir}/logo_linux_clut224.ppm drivers/video/logo/
  cp ${srcdir}/logo_linux_mono.pbm drivers/video/logo/
  cp ${srcdir}/logo_linux_vga16.ppm drivers/video/logo/


  # get kernel version  
  ARCH=powerpc make prepare
  # load configuration
  # Configure the kernel. Replace the line below with one of your choice.
  # make menuconfig # CLI menu for configuration
  #make xconfig # X-based configuration
  #make oldconfig # using old config from previous kernel version
  # ... or manually edit .config
  ####################
  # stop here
  # this is useful to configure the kernel
  # msg "Stopping build"
  # return 1
  ####################
  ARCH=powerpc make oldconfig
  # build!
  ARCH=powerpc make $MAKEFLAGS vmlinux modules
}

package_kernel26-pmac() {
  pkgdesc="The Linux Kernel and modules (32bit)"
  backup=(etc/mkinitcpio.d/${pkgname}.preset)
  provides=('kernel26')
  depends=('coreutils' 'linux-firmware>=20100911' 'module-init-tools' 'mkinitcpio>=0.5.20')
  # pwc, ieee80211 and hostap-driver26 modules are included in kernel26 now
  # nforce package support was abandoned by nvidia, kernel modules should cover everything now.
  # kernel24 support is dropped since glibc24
  replaces=('kernel24' 'kernel24-scsi' 'kernel26-scsi'
            'alsa-driver' 'ieee80211' 'hostap-driver26'
            'pwc' 'nforce' 'squashfs' 'unionfs' 'ivtv'
            'zd1211' 'kvm-modules' 'iwlwifi' 'rt2x00-cvs'
            'gspcav1' 'atl2' 'wlan-ng26' 'rt2500' 'nouveau-drm')
  install=kernel26-pmac.install
  optdepends=('crda: to set the correct wireless channels of your country')
  
  KARCH=powerpc
  cd ${srcdir}/linux-$_basekernel
  # get kernel version
  _kernver="$(ARCH=powerpc make kernelrelease)"
  mkdir -p ${pkgdir}/{lib/modules,boot}
  ARCH=powerpc make INSTALL_MOD_PATH=${pkgdir} modules_install || return 1
  cp System.map ${pkgdir}/boot/System.map26
  cp vmlinux ${pkgdir}/boot/vmlinux26
  # add vmlinux symlink
  mkdir -p ${pkgdir}/usr/src/linux-${_kernver}/
  ln -sf /boot/vmlinux26 ${pkgdir}/usr/src/linux-${_kernver}/vmlinux

  # install fallback mkinitcpio.conf file and preset file for kernel
  install -m644 -D ${srcdir}/${pkgname}.preset ${pkgdir}/etc/mkinitcpio.d/${pkgname}.preset || return 1
  # set correct depmod command for install
  sed \
    -e  "s/KERNEL_NAME=.*/KERNEL_NAME=${_kernelname}/g" \
    -e  "s/KERNEL_VERSION=.*/KERNEL_VERSION=${_kernver}/g" \
    -i $startdir/kernel26-pmac.install
  sed \
    -e "s|source .*|source /etc/mkinitcpio.d/${pkgname}.kver|g" \
    -e "s|default_image=.*|default_image=\"/boot/kernel26.img\"|g" \
    -e "s|fallback_image=.*|fallback_image=\"/boot/kernel26-fallback.img\"|g" \
    -i ${pkgdir}/etc/mkinitcpio.d/${pkgname}.preset

  echo -e "# DO NOT EDIT THIS FILE\nALL_kver='${_kernver}'" > ${pkgdir}/etc/mkinitcpio.d/${pkgname}.kver
  # remove build and source links
  rm -f ${pkgdir}/lib/modules/${_kernver}/{source,build}
  # remove the firmware
  rm -rf ${pkgdir}/lib/firmware
  
  # gzip -9 all modules to safe 100MB of space
  find "$pkgdir" -name '*.ko' -exec gzip -9 {} \;
}

package_kernel26-pmac-headers() {
  pkgdesc="Header files and scripts for building modules for kernel26"
  provides=('kernel26-headers')
  mkdir -p ${pkgdir}/lib/modules/${_kernver}
  cd ${pkgdir}/lib/modules/${_kernver}
  ln -sf ../../../usr/src/linux-${_kernver} build
  cd ${srcdir}/linux-$_basekernel
  install -D -m644 Makefile \
    ${pkgdir}/usr/src/linux-${_kernver}/Makefile
  install -D -m644 kernel/Makefile \
    ${pkgdir}/usr/src/linux-${_kernver}/kernel/Makefile
  install -D -m644 .config \
    ${pkgdir}/usr/src/linux-${_kernver}/.config
  mkdir -p ${pkgdir}/usr/src/linux-${_kernver}/include

  for i in acpi asm-generic config generated linux math-emu media net pcmcia scsi sound trace video; do
    cp -a include/$i ${pkgdir}/usr/src/linux-${_kernver}/include/
  done

  # copy arch includes for external modules
  mkdir -p ${pkgdir}/usr/src/linux-${_kernver}/arch/powerpc
  cp -a arch/powerpc/include ${pkgdir}/usr/src/linux-${_kernver}/arch/powerpc

  # copy files necessary for later builds, like nvidia and vmware
  cp Module.symvers ${pkgdir}/usr/src/linux-${_kernver}
  cp -a scripts ${pkgdir}/usr/src/linux-${_kernver}
  # fix permissions on scripts dir
  chmod og-w -R ${pkgdir}/usr/src/linux-${_kernver}/scripts
  mkdir -p ${pkgdir}/usr/src/linux-${_kernver}/.tmp_versions

  mkdir -p ${pkgdir}/usr/src/linux-${_kernver}/arch/$KARCH/kernel

  cp arch/$KARCH/Makefile ${pkgdir}/usr/src/linux-${_kernver}/arch/$KARCH/
  if [ "$CARCH" = "i686" ]; then
    cp arch/$KARCH/Makefile_32.cpu ${pkgdir}/usr/src/linux-${_kernver}/arch/$KARCH/
  fi
  cp arch/$KARCH/kernel/asm-offsets.s ${pkgdir}/usr/src/linux-${_kernver}/arch/$KARCH/kernel/

  # add headers for lirc package
  mkdir -p ${pkgdir}/usr/src/linux-${_kernver}/drivers/media/video
  cp drivers/media/video/*.h  ${pkgdir}/usr/src/linux-${_kernver}/drivers/media/video/
  for i in bt8xx cpia2 cx25840 cx88 em28xx et61x251 pwc saa7134 sn9c102 usbvision; do
   mkdir -p ${pkgdir}/usr/src/linux-${_kernver}/drivers/media/video/$i
   cp -a drivers/media/video/$i/*.h ${pkgdir}/usr/src/linux-${_kernver}/drivers/media/video/$i
  done
  # add docbook makefile
  install -D -m644 Documentation/DocBook/Makefile \
    ${pkgdir}/usr/src/linux-${_kernver}/Documentation/DocBook/Makefile
  # add dm headers
  mkdir -p ${pkgdir}/usr/src/linux-${_kernver}/drivers/md
  cp drivers/md/*.h  ${pkgdir}/usr/src/linux-${_kernver}/drivers/md
  # add inotify.h
  mkdir -p ${pkgdir}/usr/src/linux-${_kernver}/include/linux
  cp include/linux/inotify.h ${pkgdir}/usr/src/linux-${_kernver}/include/linux/
  # add wireless headers
  mkdir -p ${pkgdir}/usr/src/linux-${_kernver}/net/mac80211/
  cp net/mac80211/*.h ${pkgdir}/usr/src/linux-${_kernver}/net/mac80211/
  # add dvb headers for external modules
  # in reference to:
  # http://bugs.archlinux.org/task/9912
  mkdir -p ${pkgdir}/usr/src/linux-${_kernver}/drivers/media/dvb/dvb-core
  cp drivers/media/dvb/dvb-core/*.h ${pkgdir}/usr/src/linux-${_kernver}/drivers/media/dvb/dvb-core/
  # add dvb headers for external modules
  # in reference to:
  # http://bugs.archlinux.org/task/11194
  mkdir -p ${pkgdir}/usr/src/linux-${_kernver}/include/config/dvb/
  cp include/config/dvb/*.h ${pkgdir}/usr/src/linux-${_kernver}/include/config/dvb/
  # add dvb headers for http://mcentral.de/hg/~mrec/em28xx-new
  # in reference to:
  # http://bugs.archlinux.org/task/13146
  mkdir -p ${pkgdir}/usr/src/linux-${_kernver}/drivers/media/dvb/frontends/
  cp drivers/media/dvb/frontends/lgdt330x.h ${pkgdir}/usr/src/linux-${_kernver}/drivers/media/dvb/frontends/
  cp drivers/media/video/msp3400-driver.h ${pkgdir}/usr/src/linux-${_kernver}/drivers/media/dvb/frontends/
  # add xfs and shmem for aufs building
  mkdir -p ${pkgdir}/usr/src/linux-${_kernver}/fs/xfs
  mkdir -p ${pkgdir}/usr/src/linux-${_kernver}/mm
  cp fs/xfs/xfs_sb.h ${pkgdir}/usr/src/linux-${_kernver}/fs/xfs/xfs_sb.h
  # add headers vor virtualbox
  # in reference to:
  # http://bugs.archlinux.org/task/14568
  cp -a include/drm $pkgdir/usr/src/linux-${_kernver}/include/
  # add headers for broadcom wl
  # in reference to:
  # http://bugs.archlinux.org/task/14568
  cp -a include/trace $pkgdir/usr/src/linux-${_kernver}/include/
  # copy in Kconfig files
  for i in `find . -name "Kconfig*"`; do 
    mkdir -p ${pkgdir}/usr/src/linux-${_kernver}/`echo $i | sed 's|/Kconfig.*||'`
    cp $i ${pkgdir}/usr/src/linux-${_kernver}/$i
  done

  chown -R root.root ${pkgdir}/usr/src/linux-${_kernver}
  find ${pkgdir}/usr/src/linux-${_kernver} -type d -exec chmod 755 {} \;
  # remove unneeded architectures
  rm -rf ${pkgdir}/usr/src/linux-${_kernver}/arch/{alpha,arm,arm26,avr32,blackfin,cris,frv,h8300,ia64,m32r,m68k,m68knommu,mips,microblaze,mn10300,parisc,s390,sh,sh64,sparc,sparc64,um,v850,xtensa,x86}

  # fix builds of some external modules like nouveau drm module
  install -D -m755 ${srcdir}/linux-${_basekernel}/arch/powerpc/lib/crtsavres.o ${pkgdir}/usr/src/linux-${_kernver}/arch/powerpc/lib/crtsavres.o

  install -D -m644 ${srcdir}/linux-${_basekernel}/.config ${pkgdir}/usr/src/linux-${_basekernel}/.config
}

package_kernel26-manpages() {
pkgdesc="Kernel hackers manual - Section 9 manpages that comes with the Linux kernel."

cd ${srcdir}/linux-$_basekernel
ARCH=powerpc make mrproper
ARCH=powerpc make mandocs || return 1

install -d ${pkgdir}/usr/share/man/man9/
install ${srcdir}/linux-${_basekernel}/Documentation/DocBook/man/*.9.gz \
${pkgdir}/usr/share/man/man9/

find ${pkgdir} -type f -exec chmod 644 {} \;
}

package_kernel26-docs() {
pkgdesc="Kernel hackers manual - HTML documentation that comes with the Linux kernel."

cd ${srcdir}/linux-${_basekernel}
mkdir -p $pkgdir/usr/src/linux-${_basekernel}-ARCH
mv Documentation $pkgdir/usr/src/linux-${_basekernel}-ARCH
find $pkgdir -type f -exec chmod 444 {} \;
find $pkgdir -type d -exec chmod 755 {} \;
# remove a file already in kernel26 package
rm -f $pkgdir/usr/src/linux-${_basekernel}-ARCH/Documentation/DocBook/Makefile
}

md5sums=('1aab7a741abe08d42e8eccf20de61e05'
         'ff5eb7323c054a128d2922bde3297ed5'
         'dc02b2b541f3e717a2d18f0250a9c0ac'
         '5974286ba3e9716bfbad83d3f4ee985a'
         'e8c333eaeac43f5c6a1d7b2f47af12e2'
         'a6f0377c814da594cffcacbc0785ec1a'
         'e3d6e28acabc0a24929da1178292d634'
         'ceb38003807a5aa1a00c8d4b9f028893')
