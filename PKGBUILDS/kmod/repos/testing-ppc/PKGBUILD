# $Id$
# x86 Maintainer: Dave Reisner <dreisner@archlinux.org>

pkgname=kmod
pkgver=8
pkgrel=1
pkgdesc="Linux kernel module handling"
arch=('i686' 'x86_64' 'ppc')
url='http://git.kernel.org/?p=utils/kernel/kmod/kmod.git;a=summary'
license=('GPL2')
depends=('glibc' 'zlib')
options=('!libtool')
provides=('module-init-tools=3.16')
conflicts=('module-init-tools')
replaces=('module-init-tools')
source=("ftp://ftp.kernel.org/pub/linux/utils/kernel/$pkgname/$pkgname-$pkgver.tar.xz"
        "depmod-search.conf"
        "0001-split-usr-read-configs-from-lib-depmod.d-modprobe.d.patch"
        "0002-config-hardcode-the-path-to-modules-to-be-lib-module.patch")
md5sums=('d4e3d038b5370b1e8ff237c55666aa64'
         'dd62cbf62bd8f212f51ef8c43bec9a77'
         'ba73b9e98db1abbf41274f922fcfbd55'
         'c9af56636c5667cf4ce3a31ea56e03d9')

build() {
  cd "$pkgname-$pkgver"

  patch -p1 -i ../0001-split-usr-read-configs-from-lib-depmod.d-modprobe.d.patch
  patch -p1 -i ../0002-config-hardcode-the-path-to-modules-to-be-lib-module.patch

  ./configure \
    --sysconfdir=/etc \
    --with-zlib \
    --with-rootprefix=/usr

  make
}

check() {
  make -C "$pkgname-$pkgver" check
}

package() {
  make -C "$pkgname-$pkgver" DESTDIR="$pkgdir" install

  # extra directories
  install -dm755 "$pkgdir"/{etc,usr/lib}/{depmod,modprobe}.d "$pkgdir/sbin"

  # add symlinks to kmod
  ln -s ../usr/bin/kmod "$pkgdir/sbin/modprobe"
  ln -s ../usr/bin/kmod "$pkgdir/sbin/depmod"

  for tool in {ins,ls,rm}mod modinfo; do
    ln -s kmod "$pkgdir/usr/bin/$tool"
  done

  # install depmod.d file for search/ dir
  install -Dm644 "$srcdir/depmod-search.conf" "$pkgdir/usr/lib/depmod.d/search.conf"
}

# vim: ft=sh syn=sh et
