# $Id: PKGBUILD 111906 2011-03-01 18:29:55Z andrea $
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Alexander Baldeck <alexander@archlinux.org>
# Contributor: Lucien Immink <l.immink@student.fnt.hvu.nl>
pkgname=krusader
pkgver=2.3.0beta1
_pkgver=2.3.0-beta1
pkgrel=3
pkgdesc="An alternative file manager for KDE resembling Midnight Commander"
url="http://krusader.sourceforge.net/"
arch=('i686' 'x86_64' 'ppc')
license=('GPL')
depends=('kdebase-runtime')
makedepends=('cmake' 'automoc4' 'docbook-xsl' 'xorg-headers')
optdepends=("kdebase-keditbookmarks: to edit bookmarks")
install=${pkgname}.install
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-${_pkgver}.tar.bz2"
        'add-xz-support.patch')
md5sums=('3ef55648a2589a9a919d463acec3b830'
        'a350bc90646bd16cc4219aa30a4d1fc6')

build() {
  cd ${srcdir}/${pkgname}-${_pkgver}
  patch -Np1 -i ${srcdir}/add-xz-support.patch

  cd ${srcdir}
  mkdir build
  cd build
  cmake ../${pkgname}-${_pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd ${srcdir}/build
  make DESTDIR=${pkgdir} install
}
