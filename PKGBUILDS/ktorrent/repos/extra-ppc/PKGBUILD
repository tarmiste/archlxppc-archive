# $Id: PKGBUILD 123660 2011-05-12 07:33:28Z ronald $
# x86 Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Travis Willard <travisw@wmpub.ca>
# Contributor: Georg Grabler <ggrabler@gmail.com>
pkgname=ktorrent
pkgver=4.1.1
pkgrel=2
pkgdesc="A powerful BitTorrent client for KDE"
arch=('i686' 'x86_64' 'ppc')
url="http://ktorrent.org"
license=('GPL2')
depends=('libktorrent' 'kdebase-workspace' 'taglib')
makedepends=('automoc4' 'cmake' 'boost' 'xorg-headers')
source=("http://ktorrent.org/downloads/${pkgver}/${pkgname}-${pkgver}.tar.bz2"
        'httpserver.patch')
install=${pkgname}.install
options=('libtool')
md5sums=('affadf7c7198e5aa430e91ab75800296'
         '1f25b94615b0bbfa0457c99fb20de3e6')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	# build time patch
	patch -p0 -i "${srcdir}/httpserver.patch"

	cd "${srcdir}"
	mkdir build
	cd build
	cmake ../${pkgname}-${pkgver} \
	  -DCMAKE_BUILD_TYPE=Release \
	  -DCMAKE_INSTALL_PREFIX=/usr \
	  -DENABLE_MEDIAPLAYER_PLUGIN=true
	make
}

package() {
	cd "${srcdir}/build"
	make DESTDIR="${pkgdir}" install
}
