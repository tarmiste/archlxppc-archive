# $Id: PKGBUILD 137888 2011-09-12 08:33:12Z andrea $
# x86 Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=libdc1394
pkgver=2.1.3
pkgrel=2
pkgdesc="High level programming interface to control IEEE 1394 based cameras"
arch=('ppc' 'i686' 'x86_64')
license=('LGPL')
url="http://sourceforge.net/projects/libdc1394/"
depends=('libraw1394>=2.0.7' 'libusb')
options=('!libtool' '!emptydirs')
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        'rename-usb_init.patch'
        'videodev-compile-fix.patch')
md5sums=('d8b2cbfae1b329fdeaa638da80427334'
         'bc7a4146b4d2d688305d0d42c4111941'
         '9dd11c96241b14073c3a7dd7e270fb7a')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # FS#25652
  patch -p2 -i "${srcdir}"/rename-usb_init.patch
  # Fix build when linux/video.h exists
  patch -p2 -i "${srcdir}"/videodev-compile-fix.patch

  
  autoreconf -i
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
