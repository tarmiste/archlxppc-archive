# $Id: PKGBUILD 143769 2011-11-29 02:05:27Z eric $
# x86 Maintainer: 
# Contributor: Judd <jvinet@zeroflux.org>

pkgname=libevent
pkgver=2.0.16
pkgrel=1
pkgdesc="An event notification library"
arch=('ppc' 'i686' 'x86_64')
url="http://www.monkey.org/~provos/libevent/"
license=('GPL2')
depends=('openssl')
optdepends=('python2: to use event_rpcgen.py')
options=('!libtool')
source=(https://github.com/downloads/libevent/libevent/libevent-$pkgver-stable.tar.gz{,.asc})
sha1sums=('9eb9fe3c0ec607525ed2dee6827687efcd0696ac'
          '679023511db0106cb84a36e4ff78604d00d0a611')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}-stable"
  sed -i 's#python#python2#' event_rpcgen.py
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}-stable"
  make check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}-stable"
  make DESTDIR="${pkgdir}" install
}
