# $Id: PKGBUILD 139470 2011-10-02 01:24:23Z ibiru $
# x86 Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=libffi
pkgver=3.0.10
pkgrel=1
epoch=1
pkgdesc="A portable, high level programming interface to various calling conventions"
arch=('ppc' 'i686' 'x86_64')
url="http://sourceware.org/libffi"
license=('MIT')
depends=('glibc')
options=('!libtool')
install=libffi.install
source=(ftp://sourceware.org/pub/libffi/libffi-${pkgver}.tar.gz)
md5sums=('79390673f5d07a8fb342bc09b5055b6f')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
