# $Id: PKGBUILD 143012 2011-11-20 22:59:47Z heftig $
# x86 Maintainer: tobias <tobias@archlinux.org>
# Contributor: Tobias Kieslich <tobias@justdreams.de>

pkgname=libgda
pkgver=5.0.2
pkgrel=1
pkgdesc="Data abstraction layer with mysql, pgsql, xml, sqlite providers"
arch=('ppc' i686 x86_64)
license=('GPL')
depends=('gtksourceview3' 'libxslt' 'json-glib' 'db' 'ncurses' 'libsoup'
         'libmysqlclient' 'postgresql-libs' 'python2' 'libgnome-keyring'
         'hicolor-icon-theme' 'desktop-file-utils' 'graphviz' 'gdk-pixbuf2'
         'iso-codes' 'openssl' 'libgcrypt' 'libldap')
makedepends=('intltool' 'gobject-introspection' 'openjdk6' 'gtk-doc' 'vala')
options=('!libtool')
url="http://www.gnome-db.org"
install=libgda.install
source=(http://ftp.gnome.org/pub/GNOME/sources/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz)
sha256sums=('53e710a76298a224546803ecee711e610c68cf6938bd908e6d7e3c9bab183593')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i '1s/python$/&2/' libgda-report/RML/trml*/trml*.py
  ./configure --prefix=/usr --sysconfdir=/etc \
      --with-bdb=/usr --with-bdb-libdir-name=lib \
      --disable-static --enable-json
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
