# $Id: PKGBUILD 24494 2009-01-17 20:42:01Z jgc $
# x86 Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=libproxy
pkgver=0.4.7
pkgrel=1
pkgdesc="A library that provides automatic proxy configuration management"
arch=('ppc' i686 x86_64)
license=('LGPL')
depends=('gcc-libs')
optdepends=('kdelibs: KDE configuration module'
            'networkmanager: NetworkManager configuration module'
            'perl: Perl bindings'
            'python2: Python bindings')
makedepends=('cmake' 'networkmanager' 'automoc4' 'python2' 'kdelibs' 'perl')
url="http://libproxy.googlecode.com"
source=(http://libproxy.googlecode.com/files/${pkgname}-${pkgver}.tar.gz)
options=('!libtool')
md5sums=('509e03a488a61cd62bfbaf3ab6a2a7a5')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DPYTHON_EXECUTABLE=/usr/bin/python2 \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIBEXEC_INSTALL_DIR=/usr/lib/libproxy \
    -DCMAKE_SKIP_RPATH=ON \
    -DPERL_VENDORINSTALL=yes \
    -DCMAKE_BUILD_TYPE=Release \
    -DWITH_WEBKIT=OFF \
    -DCMAKE_CXX_FLAGS="${CXXFLAGS}" \
    -DCMAKE_C_FLAGS="${CFLAGS}"
  make
}

package() {
  cd "${srcdir}/build"
  make DESTDIR="${pkgdir}" install
}
