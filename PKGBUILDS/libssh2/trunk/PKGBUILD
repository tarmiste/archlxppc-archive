# $Id$
# x86 Maintainer: Dave Reisner <dreisner@archlinux.org>
# Contributor: Angel Velasquez <angvp@archlinux.org> 
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: ice-man <icemanf@gmail.com>

pkgname=libssh2
pkgver=1.4.1
pkgrel=1
pkgdesc="A library implementing the SSH2 protocol as defined by Internet Drafts"
url="http://www.libssh2.org/"
arch=('i686' 'x86_64' 'ppc')
license=('BSD')
depends=('openssl')
makedepends=('zlib')
provides=('libssh2.so')
options=('!libtool')
source=("http://www.libssh2.org/download/$pkgname-$pkgver.tar.gz"{,.asc})
md5sums=('b94106e046af37fdc0734e487842fe66'
         'd120a091e9f594c6608815d72b07c7a3')

build() {
  cd "$pkgname-$pkgver"

  ./configure --prefix=/usr
  make
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
