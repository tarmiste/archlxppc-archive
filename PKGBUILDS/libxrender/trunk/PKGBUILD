# $Id$
# x86 Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=libxrender
pkgver=0.9.7
pkgrel=1
pkgdesc="X Rendering Extension client library"
arch=('i686' 'x86_64' 'ppc')
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('libx11>=1.3.4' 'renderproto')
makedepends=('pkgconfig')
options=('!libtool')
source=(${url}/releases/individual/lib/libXrender-${pkgver}.tar.bz2)
sha1sums=('6e4d75d8316d5217324258f555eba3a5d383feb2')

build() {
  cd "${srcdir}/libXrender-${pkgver}"
  ./configure --prefix=/usr --disable-static
  make
}

package() {
  cd "${srcdir}/libXrender-${pkgver}"
  make DESTDIR=${pkgdir} install
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
