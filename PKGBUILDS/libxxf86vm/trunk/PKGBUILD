# $Id$
#x86 Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=libxxf86vm
pkgver=1.1.2
pkgrel=1
pkgdesc="X11 XFree86 video mode extension library"
arch=('x86_64' 'i686' 'ppc')
license=('custom')
url="http://xorg.freedesktop.org/"
depends=('libxext' 'xf86vidmodeproto')
makedepends=('xorg-util-macros')
options=('!libtool')
source=(${url}/releases/individual/lib/libXxf86vm-${pkgver}.tar.bz2)
sha1sums=('57bbf3bb69a49f6a6dec515813394a383f85502f')

build() {
  cd "${srcdir}/libXxf86vm-${pkgver}"
  ./configure --prefix=/usr --disable-static
  make
}

package()
{
  cd "${srcdir}/libXxf86vm-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
