# $Id$
# x86 Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>

pkgname=libzip
pkgver=0.10
pkgrel=2
pkgdesc="A C library for reading, creating, and modifying zip archives"
url="http://www.nih.at/libzip/index.html"
license=('GPL2')
arch=('i686' 'x86_64' 'ppc')
depends=('zlib')
options=('!libtool')
source=("http://www.nih.at/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        'fix-headers.patch')
md5sums=('47cdfcbc08c8bea0b52fdee4bf591622'
         '249395bd3a426c4c4e993e9d4753e1dd')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  patch -p1 -i "${srcdir}/fix-headers.patch"
  autoreconf -i
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR=${pkgdir} install
}
