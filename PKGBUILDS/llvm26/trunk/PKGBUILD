# $Id: PKGBUILD,v 1.4 2010/02/09 10:16:27 kth5 Exp $
# ppc Maintainer: Alexander Baldeck <alexander@archlinux.org>
# AUR Maintainer: Tomas Lindquist Olsen <tomas@famolsen.dk>
# AUR Contributor: Roberto Alsina <ralsina@kde.org>
pkgname=llvm
pkgver=2.7
pkgrel=1
pkgdesc="Low Level Virtual Machine"
arch=('i686' 'x86_64' 'ppc')
url="http://llvm.org"
license=(custom:"University of Illinois/NCSA Open Source License")
depends=('libtool' 'libelf')
source=("http://llvm.org/releases/$pkgver/$pkgname-$pkgver.tgz" LICENSE.TXT)
noextract=()
# set to 0 for a debug build
_llvm_optimized=1
#options=(!strip)

build() {
  install -D LICENSE.TXT $pkgdir/usr/share/licenses/llvm/COPYING
  cd $srcdir/$pkgname-$pkgver
  
  # --sysconfdir to configure apparently isn't enough ...
  sed -i 's:$(PROJ_prefix)/etc/llvm:/etc/llvm:' Makefile.config.in || return 1

  ./configure --prefix=/usr --sysconfdir=/etc || return 1
  
  # remove docs from the make targets
  sed -i 's/runtime docs/runtime/' Makefile || return 1
  
  make ENABLE_OPTIMIZED=$_llvm_optimized || return 1
  make ENABLE_OPTIMIZED=$_llvm_optimized DESTDIR=$pkgdir install || return 1

  # no Hello transformation please
  rm $pkgdir/usr/lib/LLVMHello*
  # docs are installed in /usr/docs which makepkg doesn't pick up, so remove it
  rm -rf $pkgdir/usr/docs
}
md5sums=('ac322661f20e7d6c810b1869f886ad9b'
         'b241a4d2e9ab2a8f5fc83d849f279640')
