# $Id: PKGBUILD 139122 2011-09-30 02:57:47Z eric $
# x86 Maintainer: Aaron Griffin <aaron@archlinux.org>
# Contributor: Judd Vinet <jvinet@zeroflux.org>

pkgname=logrotate
pkgver=3.8.1
pkgrel=1
pkgdesc="Rotates system logs automatically"
arch=('ppc' 'i686' 'x86_64')
url="https://fedorahosted.org/logrotate/"
license=('GPL')
groups=('base')
depends=('popt' 'cron' 'gzip')
backup=('etc/logrotate.conf')
source=(https://fedorahosted.org/releases/l/o/logrotate/logrotate-${pkgver}.tar.gz
        logrotate-3.8.1-noasprintf.patch
        logrotate.conf
        logrotate.cron.daily)
md5sums=('bd2e20d8dc644291b08f9215397d28a5'
         'fc8e4e15211d8aefd431d0e2e96223c4'
         '462a5f364717461537eb2ae6394ad23e'
         '8e23d5d4cc29b1e055b24df87e355cdc')

build() {
  cd "$srcdir/${pkgname}-${pkgver}"

  patch -p1 -i "$srcdir"/logrotate-3.8.1-noasprintf.patch

  sed -i 's|#define DEFAULT_MAIL_COMMAND .*|#define DEFAULT_MAIL_COMMAND "/usr/bin/mail"|'\
    config.h
  sed -i "s|CFLAGS = -Wall|CFLAGS = -Wall $CFLAGS|" Makefile
  sed -i 's|$(BASEDIR)/man|$(BASEDIR)/share/man|' Makefile
  
  make
}

check() {
  cd "$srcdir/${pkgname}-${pkgver}"
  make test
}

package() {
  cd "$srcdir/${pkgname}-${pkgver}"
  make PREFIX="$pkgdir" install

  install -Dm644 "$srcdir/logrotate.conf" "$pkgdir/etc/logrotate.conf"
  install -Dm744 "$srcdir/logrotate.cron.daily" "$pkgdir/etc/cron.daily/logrotate"
}
