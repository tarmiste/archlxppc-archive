# $Id: PKGBUILD 137665 2011-09-10 11:18:34Z pierre $
# x86 Maintainer: Alexander Baldeck <alexander@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=mcpp
pkgver=2.7.2
pkgrel=3
pkgdesc="Matsui's CPP implementation precisely conformed to standards"
arch=('ppc' 'i686' 'x86_64')
license=('custom')
url="http://mcpp.sourceforge.net"
depends=('glibc')
options=('!libtool')
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz" 
        'namlen.patch')
md5sums=('512de48c87ab023a69250edc7a0c7b05'
         '0bfc6177a58e912a9e23e368578dd3fe')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/namlen.patch"
  ./configure --prefix=/usr --enable-mcpplib --disable-static
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  ln -sf ../../doc/${pkgname}/LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/"
}
