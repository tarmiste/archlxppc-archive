# $Id: PKGBUILD 124985 2011-05-25 16:04:09Z ibiru $
# x86 Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: 03/08/04 <lefungus@altern.org>
pkgname=mkvtoolnix
pkgver=4.8.0
pkgrel=2
pkgdesc="Set of tools to create, edit and inspect Matroska files"
arch=('i686' 'x86_64' 'ppc')
license=('GPL')
url="http://www.bunkus.org/videotools/mkvtoolnix/index.html"
depends=('libmatroska' 'flac' 'libvorbis' 'file' 'wxgtk' 'boost-libs' 'lzo2' 'xdg-utils')
makedepends=('boost' 'ruby' 'xorg-headers')
install=mkvtoolnix.install
source=("http://www.bunkus.org/videotools/${pkgname}/sources/${pkgname}-${pkgver}.tar.bz2")
md5sums=('47a730706f3da2bcf4ba62bba3a8f260')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  export CFLAGS="${CFLAGS} -DBOOST_FILESYSTEM_VERSION=2"
  export CXXFLAGS="${CXXFLAGS} -DBOOST_FILESYSTEM_VERSION=2"

  ./configure --prefix=/usr \
              --with-boost-libdir=/usr/lib
  ./drake
}

package () {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./drake DESTDIR="${pkgdir}" install
}
