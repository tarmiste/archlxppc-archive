# $Id: PKGBUILD 145729 2011-12-29 13:13:45Z daniel $
# Maintainer: Daniel Isenmann <daniel@archlinux.org>
# Contributor: Brice Carpentier <brice@dlfp.org>

pkgname=mono
pkgver=2.10.8
pkgrel=1
pkgdesc="Free implementation of the .NET platform including runtime and compiler"
arch=(i686 x86_64 ppc)
license=('GPL' 'LGPL2' 'MPL' 'custom:MITX11')
url="http://www.mono-project.com/"
depends=('zlib' 'libgdiplus>=2.10' 'sh')
makedepends=('pkgconfig')
options=('!libtool' '!makeflags')
provides=('monodoc')
conflicts=('monodoc')
source=(http://download.mono-project.com/sources/${pkgname}/${pkgname}-${pkgver}.tar.gz
        mono.rc.d ppc.patch)

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  patch -p1 < ${startdir}/ppc.patch

  # build mono
  ./configure --prefix=/usr --sysconfdir=/etc \
              --with-libgdiplus=installed 
  make || return 1

  # build jay
  cd ${srcdir}/${pkgname}-${pkgver}/mcs/jay
  make || return 1
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install || return 1

  # install jay
  cd ${srcdir}/${pkgname}-${pkgver}/mcs/jay
  make DESTDIR=${pkgdir} prefix=/usr INSTALL=../../install-sh install
  cd ${srcdir}/${pkgname}-${pkgver}

  # install daemons and pathes
  mkdir -p ${pkgdir}/etc/rc.d
  install -m755 ${srcdir}/mono.rc.d ${pkgdir}/etc/rc.d/mono

  #install license
  mkdir -p ${pkgdir}/usr/share/licenses/${pkgname}
  install -m644 mcs/MIT.X11 ${pkgdir}/usr/share/licenses/${pkgname}/
  
  #fix .pc file to be able to request mono on what it depends, fixes #go-oo build
  sed -i -e "s:#Requires:Requires:" ${pkgdir}/usr/lib/pkgconfig/mono.pc
}
md5sums=('411a2d9bcfc37a61eb9aedba88b40533'
         '8315e46c6a6e9625502521fc0ad1a322'
         'c92cd06f690aed46a0b601263e2ab40e')
