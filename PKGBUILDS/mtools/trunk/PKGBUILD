# $Id: PKGBUILD 108705 2011-02-02 17:20:00Z giovanni $
# x86 Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
pkgname=mtools
pkgver=4.0.15
pkgrel=1
pkgdesc="A collection of utilities to access MS-DOS disks"
arch=('ppc' 'i686' 'x86_64')
url="http://www.gnu.org/software/mtools/"
license=('GPL')
depends=('sh')
makedepends=('texinfo' 'libx11' 'libsm' 'xorg-headers')
optdepends=('libx11: required by floppyd'
	    'libsm:  required by floppyd')
backup=('etc/mtools.conf')
install=mtools.install
source=("http://ftp.gnu.org/gnu/mtools/${pkgname}-${pkgver}.tar.bz2")
md5sums=('b7550b649af77812cb696a780e853f47')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  sed -i '/^SAMPLE FILE$/s:^:# :' mtools.conf
  ./configure --prefix=/usr \
              --mandir=/usr/share/man \
              --infodir=/usr/share/info \
	      --sysconfdir=/etc
  make || return 1
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make -j 1 DESTDIR="${pkgdir}" install || return 1
  install -Dm644 mtools.conf "${pkgdir}/etc/mtools.conf" || return 1
}
