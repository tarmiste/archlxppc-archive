# $Id: PKGBUILD 142383 2011-11-08 22:05:27Z ronald $
# x86 Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=net-tools
pkgver=1.60.20110819cvs
pkgrel=3
pkgdesc="Configuration tools for Linux networking"
arch=('ppc' i686 x86_64)
license=('GPL2')
url="http://www.tazenda.demon.co.uk/phil/net-tools"
depends=('glibc')

# http://www.tazenda.demon.co.uk/phil/$pkgname/$pkgname-$pkgver.tar.bz2
# use cvs checkout instead
source=(ftp://ftp.archlinux.org/other/net-tools/net-tools-1.60.20110819cvs.tar.bz2
  net-tools-1.60-miiioctl.patch
  net-tools-1.60-nameif_strncpy.patch)
options=(!makeflags)
install=net-tools.install
sha1sums=('02946449191ff279f9f0f7fd7bc65ae21059a2bf'
          '654a96bc6575efb4a2e04b49de45d448d240eb6e'
          '7888000d8fe867e58b5b00fe2bcf6c3041d716b4')

build() {
  cd $srcdir/$pkgname-$pkgver
  patch -Np1 -i ../net-tools-1.60-miiioctl.patch
  patch -Np1 -i ${srcdir}/net-tools-1.60-nameif_strncpy.patch
  yes "" | make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make BASEDIR=$pkgdir update

  # the following is provided by yp-tools
  rm "${pkgdir}"/bin/{nis,yp}domainname
  rm "${pkgdir}"/usr/share/man/man1/{nis,yp}domainname.1

  # hostname is provided by inetutils
  rm "${pkgdir}"/bin/{hostname,dnsdomainname,domainname}
  rm -rf "${pkgdir}"/usr/share/man/man1
}
