# Maintainer: Jouke Witteveen <j.witteveen@gmail.com>

pkgname=netcfg
pkgver=2.8.0
pkgrel=1
pkgdesc="Network configuration and profile scripts"
url="http://archlinux.org"
license=("BSD")
backup=(etc/iftab etc/conf.d/netcfg)
depends=("coreutils" "dhcpcd" "iproute2")
#makedepends=('asciidoc')  # The source tarball includes pre-built documentation.
optdepends=('dialog: for the menu based profile and wifi selectors'
            'bridge-utils: for bridge connections'
            'wpa_supplicant: for wireless networking support'
            'wpa_actiond: for automatic wireless connections through rc.d/net-auto-wireless'
            'ifplugd: for automatic wired connections through rc.d/net-auto-wired'
            'wireless_tools: for interface renaming through net-rename'
           )
source=(ftp://ftp.archlinux.org/other/netcfg/netcfg-${pkgver}.tar.xz)
arch=(any)
md5sums=('daed861ac8da3f60cfe494111b56cb06')

package() {
  cd "$srcdir/netcfg-${pkgver}"
  make DESTDIR="$pkgdir" install
  install -D -m644 LICENSE "$pkgdir/usr/share/licenses/netcfg/LICENSE"

  # Shell Completion
  install -D -m644 contrib/bash-completion "$pkgdir/usr/share/bash-completion/completions/netcfg"
  install -D -m644 contrib/zsh-completion "$pkgdir/usr/share/zsh/site-functions/_netcfg"
}

