# $Id: PKGBUILD 107621 2011-01-26 22:12:48Z andrea $
# x86 Maintainer: Andrea Scarpino <andrea@archlinux.org>
pkgname=ntrack
pkgver=009
pkgrel=1
pkgdesc="A network connectivity tracking library"
arch=('i686' 'x86_64' 'ppc')
url="http://launchpad.net/ntrack/"
license=('LGPL')
depends=('qt' 'libnl' 'gcc-libs')
makedepends=('python2' 'xorg-headers')
optdepends=('python2')
source=("http://launchpad.net/${pkgname}/main/${pkgver}/+download/${pkgname}-${pkgver}.tar.gz")
md5sums=('37e35967a03962f4d2d40ec0816250af')
options=('!libtool')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
