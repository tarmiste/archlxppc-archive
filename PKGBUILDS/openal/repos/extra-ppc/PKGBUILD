# $Id: PKGBUILD 129235 2011-06-26 06:15:14Z heftig $
# x86 Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributer: Jason Chu <jchu@xentac.net>

pkgname=openal
pkgver=1.13
pkgrel=2
pkgdesc="A cross-platform 3D audio library"
arch=('ppc' i686 x86_64)
url="http://www.openal.org/"
license=(LGPL)
depends=(glibc)
makedepends=(alsa-lib sdl pkgconfig cmake libpulse)
options=('!libtool')
source=(http://kcat.strangesoft.net/openal-releases/openal-soft-$pkgver.tar.bz2)
md5sums=('58b7d2809790c70681b825644c5f3614')

build() {
  cd "$srcdir/$pkgname-soft-$pkgver/build"
  cmake -D CMAKE_INSTALL_PREFIX=/usr -D CMAKE_BUILD_TYPE=Release ..
  make
}

package() {
  cd "$srcdir/$pkgname-soft-$pkgver"
  make -C build DESTDIR="$pkgdir/" install
  install -Dm644 alsoftrc.sample "$pkgdir/etc/openal/alsoft.conf.sample"
}
