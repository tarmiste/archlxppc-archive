# $Id$
# x86 Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Tobias Kieslich <tobias (at) archlinux.org>

pkgname=orage
pkgver=4.8.3
pkgrel=2
pkgdesc="A simple calendar application with reminders for Xfce"
arch=('i686' 'x86_64' 'ppc')
license=('GPL2')
url="http://www.xfce.org/projects/orage/"
groups=('xfce4-goodies')
depends=('xfce4-panel' 'libical' 'popt' 'libnotify>=0.7.1' 'hicolor-icon-theme' 'desktop-file-utils')
makedepends=('intltool' 'xfce4-dev-tools' 'popt')
options=('!libtool')
replaces=('xfcalendar')
install=${pkgname}.install
source=(http://archive.xfce.org/src/apps/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.bz2)
sha1sums=('f4f89ea1f301514c419e7643661b510132ebb012')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
	--sysconfdir=/etc \
	--libexecdir=/usr/lib \
	--localstatedir=/var \
	--enable-libical \
	--disable-static \
	--disable-debug 
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make -j1 DESTDIR="${pkgdir}" install
}
