# Packager: Justin Davis <jrcd83@gmail.com>
# $Id$

pkgname=perl-anyevent
pkgver=6.13
pkgrel=1
pkgdesc="the DBI of event loop programming"
epoch=2
arch=(any)
license=(PerlArtistic GPL)
options=(!emptydirs)
depends=('perl' 'perl-ev>=4.00' 'perl-guard>=1.02' 'perl-async-interrupt>=1.0')
optdepends=('perl-net-ssleay: TLS/SSL connections with AnyEvent::Handle'
            'perl-json-xs: send or receive JSON with AnyEvent::Handle')
url=https://metacpan.org/release/AnyEvent
source=("http://search.cpan.org/CPAN/authors/id/M/ML/MLEHMANN/AnyEvent-${pkgver}.tar.gz")
md5sums=(f20be873d23eb0c72dcf909908cafea4)
sha512sums=(eab285fa45f4c728ee8943e2798b61e7b12d5e5e8e400c4f1b4b87e339f0ce566e5018cbffd2ad58825a8ececa41490f20befce74023992ce58b77f15bea2563)
_distdir="${srcdir}/AnyEvent-${pkgver}"

build() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
      PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
      MODULEBUILDRC=/dev/null

    cd "$_distdir"
    /usr/bin/perl Makefile.PL
    make
  )
}

check() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    cd "$_distdir"
    make test
  )
}

package() {
    cd "$_distdir"
    make DESTDIR="$pkgdir" install
    find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
