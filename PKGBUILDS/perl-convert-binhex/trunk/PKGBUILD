# $Id$
# x86 Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Techlive Zheng <techlivezheng [at] gmail [dot] com>
# Contributor: Jakub Fi≈°er <mr [at] MikyMaus [dot] org>
# Contributor: Filip S. Adamsen <fsa [at] fsadev [dot] com>

pkgname=perl-convert-binhex
pkgver=1.119
pkgrel=4
pkgdesc="Perl module to extract data from Macintosh BinHex files"
arch=('any')
url="http://search.cpan.org/~eryq/Convert-BinHex-1.119/"
license=('PerlArtistic')
depends=('perl')
options=('!emptydirs')
install=${pkgname}.install
source=("http://search.cpan.org/CPAN/authors/id/E/ER/ERYQ/Convert-BinHex-$pkgver.tar.gz")
md5sums=('ba70ad1772abac6270078f28197a7961')

build() {
  cd "${srcdir}/Convert-BinHex-${pkgver}"

  # install module in vendor directories.
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

package() {
  cd "${srcdir}/Convert-BinHex-${pkgver}"

  make DESTDIR="${pkgdir}/" install

  # remove perllocal.pod and .packlist
  find "${pkgdir}" -name perllocal.pod -delete
  find "${pkgdir}" -name .packlist -delete
}
