# $Id$
# x86 Maintainer: tobias <tobias@archlinux.org>
# Contributor: Tobias Kieslich <tobias@justdreams.de>
# Contributor: Fran√ßois Charette <francois.archlinux.org>

pkgname=perl-event
_srcname=Event
pkgver=1.20
pkgrel=1
pkgdesc="Framework for GUI events"
arch=('i686' 'x86_64' 'ppc')
license=('PerlArtistic' 'GPL')
url="http://search.cpan.org/dist/${_srcname}/"
depends=('perl')
options=('!emptydirs')
source=(http://search.cpan.org/CPAN/authors/id/J/JP/JPRIT/${_srcname}-${pkgver}.tar.gz)
md5sums=('bdbdf6c1b235c3e13c25f3b0491c5134')

build() {
  cd "${srcdir}/${_srcname}-${pkgver}"

  # install module in vendor directories.
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "${srcdir}/${_srcname}-${pkgver}"
  make test
}

package() {
  cd "${srcdir}/${_srcname}-${pkgver}"

  make install DESTDIR="${pkgdir}"

  # remove perllocal.pod and .packlist
  find "${pkgdir}" -name perllocal.pod -delete
  find "${pkgdir}" -name .packlist -delete
}
