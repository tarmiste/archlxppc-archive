# $Id$
# x86 Maintainer: kevin <kevin@archlinux.org>

pkgname=perl-file-desktopentry
_realname=File-DesktopEntry
pkgver=0.04
pkgrel=5
pkgdesc='Object to handle .desktop files'
arch=('any')
license=('GPL' 'PerlArtistic')
url="http://search.cpan.org/dist/${_realname}/"
depends=('perl-file-basedir')
options=(!emptydirs)
source=(http://search.cpan.org/CPAN/authors/id/P/PA/PARDUS/${_realname}/${_realname}-$pkgver.tar.gz)
md5sums=('fc196bc1a4d6ca84953fee5a9cdaa629')

build() {
  cd ${srcdir}/${_realname}-$pkgver
  # install module in vendor directories.
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd ${srcdir}/${_realname}-$pkgver
  make test
}

package() {
  cd ${srcdir}/${_realname}-$pkgver
  make install DESTDIR=${pkgdir}
}
