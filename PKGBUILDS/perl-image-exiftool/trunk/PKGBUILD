# $Id: PKGBUILD 144999 2011-12-13 23:07:09Z eric $
# x86 Maintainer: Florian Pritz <bluewind@xinu.at>
# Contributor: tobias <tobias@archlinux.org>
# Contributor: Tobias Kieslich <tobias@justdreams.de>
# Contributor: Firmicus <francois.archlinux.org>

pkgname=perl-image-exiftool
_srcname=Image-ExifTool
pkgver=8.79
pkgrel=1
pkgdesc="Reader and rewriter of EXIF informations that supports raw files"
arch=('any')
url="http://www.sno.phy.queensu.ca/~phil/exiftool/"
license=('GPL')
depends=('perl')
provides=("perl-exiftool=$pkgver")
replaces=("perl-exiftool")
options=('!emptydirs')
source=(http://www.sno.phy.queensu.ca/~phil/exiftool/${_srcname}-$pkgver.tar.gz)
md5sums=('72a9095587f3b69efa0b189428512326')

build() {
  cd "${srcdir}/${_srcname}-${pkgver}"
  # install module in vendor directories.
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "${srcdir}/${_srcname}-${pkgver}"
  make test
}

package() {
  cd "${srcdir}/${_srcname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  # remove *.pod, .packlist, and empty dirs:
  find "${pkgdir}" -name '.packlist' -delete
  find "${pkgdir}" -name '*.pod' -delete
}

