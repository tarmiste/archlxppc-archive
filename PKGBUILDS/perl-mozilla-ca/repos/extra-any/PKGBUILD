# Packager: Justin "juster" Davis <jrcd83@gmail.com>
# $Id$

pkgname=perl-mozilla-ca
pkgver=20120118
pkgrel=1
pkgdesc="Mozilla's CA cert bundle in PEM format"
arch=(any)
license=(GPL2 LGPL2.1)
options=(!emptydirs)
depends=('perl>=5.006')
url=https://metacpan.org/release/Mozilla-CA
source=("http://search.cpan.org/CPAN/authors/id/A/AB/ABH/Mozilla-CA-${pkgver}.tar.gz")
md5sums=(e1c650d25303bf3d59a6502efd002b06)
sha512sums=(89f50f39ff6fe4eda33a659530fd3383ccc46bc05a4827bc8701557f645f7148bfbcc4bed890f1f1f903c3214dceb017e1d7e8df6c45d51e4d1f327b68b3b065)
_distdir="${srcdir}/Mozilla-CA-${pkgver}"

build()
(
    export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
      PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
      MODULEBUILDRC=/dev/null

    cd "$_distdir"
    /usr/bin/perl Makefile.PL
    make
)

check()
(
    export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    cd "$_distdir"
    make test
)

package() {
    cd "$_distdir"
    make DESTDIR="$pkgdir" install
    find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
