# $Id: PKGBUILD 125273 2011-05-25 19:19:34Z foutrelis $
# x86 Maintainer: kevin <kevin@archlinux.org>
# Contributor: Manolis Tzanidakis

pkgname=perl-net-dns
_realname=Net-DNS
pkgver=0.66
pkgrel=4
pkgdesc="Perl Module: Interface to the DNS resolver."
arch=('ppc' i686 x86_64)
license=('PerlArtistic')
url="http://search.cpan.org/dist/${_realname}/"
depends=('perl-digest-hmac' 'perl-net-ip' 'perl>=5.10.0')
source=(http://www.cpan.org/authors/id/O/OL/OLAF/${_realname}-$pkgver.tar.gz)
options=(!emptydirs)

build() {
  cd $srcdir/${_realname}-${pkgver}
  # install module in vendor directories.
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

package() {
  cd $srcdir/${_realname}-${pkgver}
  make install DESTDIR=${pkgdir}

  # remove perllocal.pod and .packlist
  find ${pkgdir} -name perllocal.pod -delete
  find ${pkgdir} -name .packlist -delete
}
# vim: ts=2 sw=2 et ft=sh
md5sums=('1635d876324e3c2f6e277d5778bfe94c')
