# Packager: Justin "juster" Davis <jrcd83@gmail.com>
# $Id$

pkgname=perl-net-server
pkgver=0.99
pkgrel=2
pkgdesc="Extensible, general Perl server engine"
arch=(any)
license=(PerlArtistic GPL)
options=(!emptydirs)
depends=('perl')
url=https://metacpan.org/release/Net-Server
source=("http://search.cpan.org/CPAN/authors/id/R/RH/RHANDOM/Net-Server-${pkgver}.tar.gz")
md5sums=(1ae03dff8b1009216a2e5d8f4c9a47b1)
sha512sums=(e9d233657a116cab054f269f73e0c341d265871238076850d43e1fbeb9a99e5abedf09a1c5f95834ef14de834c993f772820a380083c8a28b174360b4b8056fe)
_distdir="${srcdir}/Net-Server-${pkgver}"

build()
(
    export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
      PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
      MODULEBUILDRC=/dev/null

    cd "$_distdir"
    /usr/bin/perl Makefile.PL
    make
)

check()
(
    export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    cd "$_distdir"
    make test
)

package() {
    cd "$_distdir"
    make DESTDIR="$pkgdir" install
    find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
