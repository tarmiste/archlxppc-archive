# $Id: PKGBUILD 143942 2011-11-30 21:49:37Z giovanni $
# x86 Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Manolis Tzanidakis

pkgname=perl-net-ssleay
_cpanname=Net-SSLeay
pkgver=1.42
pkgrel=2
pkgdesc="Perl extension for using OpenSSL"
arch=('ppc' 'i686' 'x86_64')
license=('custom:BSD')
url="http://search.cpan.org/dist/${_cpanname}/"
depends=('perl' 'openssl')
options=(!emptydirs)
replaces=('net-ssleay')
provides=('net-ssleay')
source=("http://www.cpan.org/authors/id/M/MI/MIKEM/${_cpanname}-${pkgver}.tar.gz")
md5sums=('3f9c9589495301c14bfc03097388ca4f')

build() {
  cd ${srcdir}/${_cpanname}-${pkgver}
  # install module in vendor directories.
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL -- INSTALLDIRS=vendor 
  make
}

package() {
  cd ${srcdir}/${_cpanname}-${pkgver}

  make install DESTDIR=${pkgdir} 

  # remove perllocal.pod and .packlist
  find ${pkgdir} -name perllocal.pod -delete
  find ${pkgdir} -name .packlist -delete

  install -D -m644 README "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
