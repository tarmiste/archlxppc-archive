# $Id$
# x86 Maintainer: Kevin Piche <kevin@archlinux.org>
# Contributor: Manolis Tzanidakis

pkgname=perl-timedate
_realname=TimeDate
pkgver=1.20
pkgrel=3
pkgdesc="Date formating subroutines"
arch=('any')
license=('PerlArtistic')
url="http://search.cpan.org/dist/${_realname}/"
depends=('perl')
source=("http://www.cpan.org/authors/id/G/GB/GBARR/${_realname}-${pkgver}.tar.gz")
options=('!emptydirs')
replaces=('timedate')
provides=('timedate')
md5sums=('7da7452bce4c684e4238e6d09b390200')

build() {
  cd $srcdir/${_realname}-${pkgver}

  # install module in vendor directories.
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd $srcdir/${_realname}-${pkgver}
  make test
}

package() {
  cd $srcdir/${_realname}-${pkgver}
  make install DESTDIR=${pkgdir}

  # remove perllocal.pod and .packlist
  find ${pkgdir} -name perllocal.pod -delete
  find ${pkgdir} -name .packlist -delete
}
# vim: ts=2 sw=2 et ft=sh
