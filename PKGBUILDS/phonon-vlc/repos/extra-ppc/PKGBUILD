# $Id: PKGBUILD 134014 2011-08-01 08:35:33Z andrea $
# x86 Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=phonon-vlc
pkgver=0.4.1
pkgrel=1
arch=('ppc' 'i686' 'x86_64')
url="http://phonon.kde.org"
pkgdesc="Phonon VLC backend"
license=('LGPL')
depends=('vlc')
makedepends=('cmake' 'automoc4' 'phonon')
provides=('phonon-backend')
source=("http://download.kde.org/stable/phonon/phonon-backend-vlc/${pkgver}/phonon-backend-vlc-${pkgver}.tar.bz2")
md5sums=('bdf188c0d3ad3458e7e39ecad06a500b')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../phonon-backend-vlc-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
