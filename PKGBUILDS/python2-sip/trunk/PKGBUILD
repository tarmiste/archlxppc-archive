# $Id: PKGBUILD 107403 2011-01-24 17:30:04Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>
# Contributor: riai <riai@bigfoot.com>, Ben <ben@benmazer.net>

pkgname=python2-sip
pkgver=4.12.1
pkgrel=1
pkgdesc="A tool that makes it easy to create Python 2 bindings for C and C++ libraries"
arch=('i686' 'x86_64' 'ppc')
url="http://www.riverbankcomputing.com/software/sip/"
license=('custom:"sip"')
depends=('python2')
replaces=('sip')
source=("http://www.riverbankcomputing.com/static/Downloads/sip4/sip-${pkgver}.tar.gz")
md5sums=('0f8e8305b14c1812191de2e0ee22fea9')

build() {
  cd "${srcdir}/sip-${pkgver}"

  python2 configure.py CFLAGS="${CFLAGS}" LFLAGS="${LDFLAGS}"
  make
}

package() {
  cd "${srcdir}/sip-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
