# $Id: PKGBUILD 142933 2011-11-18 14:00:57Z pierre $
# x86 Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=reiserfsprogs
pkgver=3.6.21
pkgrel=4
pkgdesc="Reiserfs utilities"
arch=('ppc' 'i686' 'x86_64')
url="http://www.kernel.org/"
license=('GPL')
groups=('base')
depends=('util-linux-ng')
source=("ftp://ftp.archlinux.org/other/reiserfsprogs/reiserfsprogs-${pkgver}.tar.bz2")
md5sums=('0639cefac8f8150536cfa7531c2aa2d2')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sbindir=/sbin
  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  ln -s reiserfsck "${pkgdir}/sbin/fsck.reiserfs"
  ln -s mkreiserfs "${pkgdir}/sbin/mkfs.reiserfs"
}
