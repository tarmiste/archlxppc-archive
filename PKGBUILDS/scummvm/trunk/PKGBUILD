# $Id: PKGBUILD 106447 2011-01-17 06:22:16Z angvp $
# x86 Maintainer: juergen <juergen@hoetzel.info>
# Contributor: Manolis Tzanidakis, Petter Gundersen
pkgname=scummvm
pkgver=1.2.1
pkgrel=1
pkgdesc="A 'virtual machine' for several classic graphical point-and-click adventure games."
arch=('i686' 'x86_64' 'ppc')
license=('GPL')
url="http://www.scummvm.org/"
depends=('gcc-libs' 'alsa-lib' 'libmad' 'libvorbis' 'zlib' 'flac>=1.1.4' 'sdl' 'fluidsynth')
makedepends=('xorg-headers')
source=(http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.bz2)
md5sums=('b1d224d7640bc7fdccaca958164b31df')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --backend=sdl --disable-debug \
    --with-mpeg2-prefix=/usr \
    --with-flac-prefix=/usr \
    --with-fluidsynth-prefix=/usr \
    --prefix=/usr
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  install -Dm755 scummvm ${pkgdir}/usr/bin/scummvm
  install -Dm644 dists/scummvm.6 ${pkgdir}/usr/share/man/man6/scummvm.6
  # fixes FS#9118 
  install -Dm644 dists/${pkgname}.desktop \
	${pkgdir}/usr/share/applications/${pkgname}.desktop
  install -Dm644  icons/scummvm.xpm \
	${pkgdir}/usr/share/pixmaps/scummvm.xpm

  install -Dm644 gui/themes/scummmodern.zip ${pkgdir}/usr/share/scummvm/scummmodern.zip

}
