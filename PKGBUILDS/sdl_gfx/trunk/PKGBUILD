# $Id: PKGBUILD 144350 2011-12-04 22:59:47Z heftig $
# x86 Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=sdl_gfx
pkgver=2.0.23
pkgrel=1
pkgdesc="SDL Graphic Primitives"
arch=('ppc' 'i686' 'x86_64')
license=('LGPL')
options=(!libtool)
url="http://www.ferzkopp.net/joomla/software-mainmenu-14/4-ferzkopps-linux-software/19-sdlgfx"
source=(http://www.ferzkopp.net/Software/SDL_gfx-2.0/SDL_gfx-${pkgver}.tar.gz)
depends=('sdl')
md5sums=('fcc3c4f2d1b4943409bf7e67dd65d03a')

build() {
  cd ${srcdir}/SDL_gfx-${pkgver}

  if [ "${CARCH}" == "x86_64" -o "${CARCH}" == "ppc" ]; then
    ./configure --prefix=/usr --disable-mmx
  else
    ./configure --prefix=/usr
  fi

  make
}

package() {
  cd ${srcdir}/SDL_gfx-${pkgver}
  make DESTDIR=${pkgdir} install
}
