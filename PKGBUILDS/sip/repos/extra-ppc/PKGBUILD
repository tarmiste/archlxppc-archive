# $Id$
# x86 Maintainer:
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>
# Contributor: riai <riai@bigfoot.com>, Ben <ben@benmazer.net>

pkgbase=sip
pkgname=('sip' 'python2-sip')
pkgver=4.13.1
pkgrel=1
arch=('ppc' 'i686' 'x86_64')
url="http://www.riverbankcomputing.com/software/sip/"
license=('custom:"sip"')
makedepends=('python' 'python2')
source=("http://www.riverbankcomputing.com/static/Downloads/sip4/${pkgbase}-${pkgver}.tar.gz")

build() {
  cd "${srcdir}"
  cp -r ${pkgbase}-${pkgver} python2-${pkgbase}-${pkgver}

  cd "${srcdir}/${pkgbase}-${pkgver}"
  python configure.py CFLAGS="${CFLAGS}" LFLAGS="${LDFLAGS}"
  make
  
  ### Python2 version ###
  cd "${srcdir}/python2-${pkgbase}-${pkgver}"
  python2 configure.py CFLAGS="${CFLAGS}" LFLAGS="${LDFLAGS}"
  make
}

package_sip() {
  pkgdesc="A tool that makes it easy to create Python bindings for C and C++ libraries"
  depends=('python')
  replaces=('python-sip')
  provides=('python-sip')

  cd "${srcdir}/${pkgbase}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-sip() {
  pkgdesc="A tool that makes it easy to create Python2 bindings for C and C++ libraries"
  depends=('sip' 'python2')

  cd "${srcdir}/python2-${pkgbase}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  # Provided by sip
  rm "${pkgdir}/usr/bin/sip"
}
md5sums=('3f61fd39d5b0c8fa9e43b59af04de924')
