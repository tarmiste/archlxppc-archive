# $Id: PKGBUILD 136035 2011-08-21 23:31:51Z eric $
# x86 Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=soundtouch
pkgver=1.6.0
pkgrel=1
pkgdesc="An audio processing library"
arch=('ppc' 'i686' 'x86_64')
url="http://www.surina.net/soundtouch/"
license=('LGPL')
depends=('gcc-libs')
options=('!libtool')
source=(http://www.surina.net/soundtouch/${pkgname}-${pkgver}.tar.gz
        soundtouch-1.4.0-mmx-sse-compile-fix.patch
        soundtouch-1.4.0-x86_64-asm-broken.patch)

build() {
  cd "${srcdir}/${pkgname}"
  patch -p1 -i "${srcdir}/soundtouch-1.4.0-mmx-sse-compile-fix.patch"
  patch -p1 -i "${srcdir}/soundtouch-1.4.0-x86_64-asm-broken.patch"

  [ "${CARCH}" = "x86_64" ] && CONFIGFLAG="--with-pic"
  ./bootstrap
  ./configure --prefix=/usr --enable-shared ${CONFIGFLAG}
  make
}

package() {
  cd "${srcdir}/${pkgname}"
  make DESTDIR="${pkgdir}" pkgdocdir=/usr/share/doc/soundtouch install
}
md5sums=('aa1c63d4d67b033f044a6a48d2be5bdd'
         '110c71b048146c8ede1394363b82d331'
         '0d856bb5b519b18c6ba3a2e1770bdfd2')
