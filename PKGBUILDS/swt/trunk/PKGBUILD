# $Id: PKGBUILD,v 1.5 2010/02/09 10:16:27 kth5 Exp $
# x86 Maintainer: simo <simo@archlinux.org>
pkgname=swt
pkgver=3.5.1
pkgrel=1
pkgdesc="An open source widget toolkit for Java"
_date=200909170800
arch=(i686 x86_64 ppc)
url="http://www.eclipse.org/swt/"
license=('EPL')
depends=('openjdk-6-jre' 'gtk2>=2.18.3' 'libxtst')
optdepends=('libgnomeui' 'xulrunner' 'mesa')
makedepends=('openjdk-6-sdk' 'libxtst' 'mesa' 'libgnomeui' 'xulrunner>=1.9.1' 'unzip' 'pkgconfig' 'apache-ant'
             'xorg-headers')
source=(http://download.eclipse.org/eclipse/downloads/drops/R-${pkgver}-${_date}/swt-${pkgver}-gtk-linux-ppc.zip
        build-swt.xml)

build() {
  cd "${srcdir}"
  . /etc/profile.d/openjdk-6-jre.sh
  . /etc/profile.d/apache-ant.sh
  unzip -jqo ${srcdir}/swt-${pkgver}-gtk-linux-*.zip "*src.zip"
  unzip -qo src.zip
  rm -rf about_files
  mkdir src
  mv org src/
  cp build-swt.xml build.xml || return 1

  [ "${CARCH}" = "x86_64" ] && export SWT_PTR_CFLAGS=-DSWT_PTR_SIZE_64

  make -f make_linux.mak make_awt || return 1
  make -f make_linux.mak make_swt || return 1
  make -f make_linux.mak make_atk || return 1
  make -f make_linux.mak make_gnome || return 1
  export MOZILLA_INCLUDES="`pkg-config --cflags libxul`"
  export MOZILLA_LIBS="-Wl,-R`pkg-config libxul --variable=libdir` `pkg-config libxul --libs`"
  export XULRUNNER_INCLUDES="${MOZILLA_INCLUDES}"
  export XULRUNNER_LIBS="${MOZILLA_LIBS}"
  make -f make_linux.mak make_mozilla || return 1
  make -f make_linux.mak make_xulrunner || return 1

  make -f make_linux.mak make_cairo || return 1
  make -f make_linux.mak make_glx || return 1
 
  ant compile || return 1
  cp version.txt build/version.txt
  cp src/org/eclipse/swt/internal/SWTMessages.properties build/org/eclipse/swt/internal/

  ant jar || return 1

  install -m755 -d "${pkgdir}/usr/share/java"
  install -m755 -d "${pkgdir}/usr/lib"
  install -m644 swt.jar "${pkgdir}/usr/share/java/" || return 1
  install -m755 *.so "${pkgdir}/usr/lib/" || return 1
}
md5sums=('562a68de173331567a364fcc6411243b'
         'f5e548bc26a0f1f3c18131be76face40')
