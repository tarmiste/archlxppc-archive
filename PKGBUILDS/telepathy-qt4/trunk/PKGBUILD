# $Id: PKGBUILD 142995 2011-11-20 09:11:34Z ibiru $
# x86 Maintainer: Ionut Biru <ibiru@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=telepathy-qt4
pkgver=0.8.0
pkgrel=1
pkgdesc="A library for Qt-based Telepathy clients"
arch=('ppc' 'i686' 'x86_64')
url="http://telepathy.freedesktop.org/wiki/"
license=('LGPL')
options=('!libtool')
depends=('qt' 'telepathy-farsight')
makedepends=('libxslt' 'python2' 'cmake' 'doxygen')
source=("http://telepathy.freedesktop.org/releases/$pkgname/$pkgname-$pkgver.tar.gz")
md5sums=('b93f03f063d784855d83e1b3c79a1cc5')

build() {
    cd "$srcdir"
    mkdir build
    cd build
    cmake ../$pkgname-$pkgver \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DPYTHON_EXECUTABLE=/usr/bin/python2
    make
}

package() {
    cd "$srcdir"/build
    make DESTDIR="$pkgdir" install
}
