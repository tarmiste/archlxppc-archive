# $Id: PKGBUILD 157826 2012-04-30 04:19:44Z foutrelis $
# x86 Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: tobias <tobias funnychar archlinux.org>
# Contributor: Aurelien Foret <orelien@chez.com>

pkgname=terminal
pkgver=0.4.8
pkgrel=2
pkgdesc="A modern terminal emulator primarly for the Xfce desktop environment"
arch=('ppc' 'i686' 'x86_64')
url="http://www.xfce.org/projects/terminal/"
license=('GPL2')
groups=('xfce4')
depends=('exo' 'vte' 'dbus-glib' 'startup-notification' 'hicolor-icon-theme')
makedepends=('pkgconfig' 'intltool')
options=('!libtool')
install=$pkgname.install
source=(http://archive.xfce.org/src/apps/$pkgname/0.4/Terminal-$pkgver.tar.bz2)
sha256sums=('fe5db0b7127ce87efa1e10f44d8915be88005b6ce4f0bfea6b64533e25cc788d')

build() {
  cd "$srcdir/Terminal-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/xfce4 \
    --localstatedir=/var \
    --disable-static \
    --disable-debug
  make
}

package() {
  cd "$srcdir/Terminal-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
