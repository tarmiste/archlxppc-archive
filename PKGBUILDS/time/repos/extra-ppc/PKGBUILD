# $Id$
# x86 Maintainer: St√©phane Gaudreault <stephane@archlinux.us>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: damir <damir@archlinux.org>

pkgname=time
pkgver=1.7
pkgrel=6
pkgdesc="Utility for monitoring a program's use of system resources"
arch=('i686' 'x86_64' 'ppc')
url="http://www.gnu.org/directory/time.html"
license=('GPL')
depends=('glibc')
install=time.install
source=(http://ftp.gnu.org/pub/gnu/${pkgname}/${pkgname}-${pkgver}.tar.gz
        time-1.7-Recompute-CPU-usage-at-microsecond-level.patch
        time-1.7-ru_maxrss-is-in-kilobytes-on-Linux.patch)
sha1sums=('dde0c28c7426960736933f3e763320680356cc6a'
          'fff80e05dd41f0a59bb1f49d1c6e581d5e8e92e5'
          'b2a909e1d92f7af1bc99a4524ad84d467ed79a06')


build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
   
   # cf https://bugzilla.redhat.com/show_bug.cgi?id=527276
   patch -Np1 -i ../time-1.7-Recompute-CPU-usage-at-microsecond-level.patch

   # cf https://bugzilla.redhat.com/show_bug.cgi?id=702826
   patch -Np1 -i ../time-1.7-ru_maxrss-is-in-kilobytes-on-Linux.patch

	./configure --prefix=/usr --infodir=/usr/share/info
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	make exec_prefix="${pkgdir}/usr" infodir="${pkgdir}/usr/share/info" install
}
