# $Id: PKGBUILD 39243 2011-02-07 19:01:52Z lfleischer $
# x86 Maintainer: Lukas Fleischer <archlinux at cryptocrack dot de>
# Contributor: simo <simo@archlinux.org>
pkgname=tor
pkgver=0.2.1.29
pkgrel=2
pkgdesc='Anonymizing overlay network.'
arch=('i686' 'x86_64' 'ppc')
url='http://www.torproject.org/'
license=('BSD')
depends=('openssl' 'libevent>=2.0.10' 'tsocks' 'bash')
backup=('etc/tor/torrc'
        'etc/tor/torrc-dist'
        'etc/tor/tor-tsocks.conf'
        'etc/conf.d/tor')
install='tor.install'
source=("http://www.torproject.org/dist/${pkgname}-${pkgver}.tar.gz"
        'torrc'
        'tor'
        'tor.conf.d')
md5sums=('1cd4feea84f2b066717b500d090bcf65'
         '56c75d4e8a66f34167d31e38c43793dd'
         'f8e6868a389877346e7eebaacd1078bb'
         '5c7c6834064b3530c442def6079ac3aa')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  install -dm0700 "${pkgdir}/var/lib/tor"

  mv "${pkgdir}/etc/tor/torrc.sample" "${pkgdir}/etc/tor/torrc-dist"
  install -Dm0644 "${srcdir}/torrc" "${pkgdir}/etc/tor/torrc"
  install -Dm0755 "${srcdir}/tor" "${pkgdir}/etc/rc.d/tor"
  install -Dm0644 "${srcdir}/tor.conf.d" "${pkgdir}/etc/conf.d/tor"

  install -Dm0644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
