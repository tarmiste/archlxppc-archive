# $Id$
# x86 Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname=tsocks
pkgver=1.8beta5
pkgrel=5
pkgdesc='Transparent SOCKS proxying library'
url='http://tsocks.sourceforge.net/'
license=('GPL')
arch=('i686' 'x86_64' 'ppc')
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha1sums=('489f88c5df999ba21b27cdaa7836d9426d062aec')

build() {
	cd "${srcdir}/${pkgname}-1.8"
	./configure --prefix=/usr --sysconfdir=/etc --mandir=/usr/share/man --libdir=/usr/lib
	make
}

package() {
	cd "${srcdir}/${pkgname}-1.8"
	make DESTDIR="${pkgdir}" install
	install -d "${pkgdir}"/etc
	install -m644 tsocks.conf.{simple,complex}.example "${pkgdir}"/etc
}
