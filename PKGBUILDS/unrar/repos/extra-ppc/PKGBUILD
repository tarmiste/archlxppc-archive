# $Id$
# x86 Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: dorphell <dorphell@gmx.net>

pkgbase=unrar
pkgname=('unrar' 'libunrar')
pkgver=4.1.4
pkgrel=2
arch=('i686' 'x86_64' 'ppc')
url="http://www.rarlab.com/rar_add.htm"
license=('custom')
makedepends=('gcc-libs')
source=("http://www.rarlab.com/rar/unrarsrc-${pkgver}.tar.gz"
        'unrar-4.1.4-types.patch'
        'unrar-4.1.4-gcc.patch'
        'unrar-4.1.4-variablesize.patch'
        'unrar-4.1.4-soname.patch')
md5sums=('808c92a661820a637ca1330cf40c18e4'
         'ce5396b4cce009962c393a34c81e8f99'
         '7af8795807b9d5c9e29b240be643223c'
         '72df7e8d7049ed12a228f172c42eccb1'
         '90935a3f6a55f2c3b85e2fd0c10a4d86')

build() {
  cd "${srcdir}/${pkgbase}"

  patch -Np0 -i "${srcdir}/unrar-4.1.4-types.patch"
  patch -Np0 -i "${srcdir}/unrar-4.1.4-gcc.patch"
  patch -Np0 -i "${srcdir}/unrar-4.1.4-variablesize.patch"
  patch -Np0 -i "${srcdir}/unrar-4.1.4-soname.patch"

  cp -a ${srcdir}/unrar ${srcdir}/libunrar
  make -C "${srcdir}"/libunrar -f makefile.unix lib libversion=${pkgver} CXXFLAGS="-fPIC ${CXXFLAGS}"
  make -f makefile.unix CXXFLAGS="-fPIC ${CXXFLAGS}" STRIP="true"
}

package_unrar() {
  pkgdesc="The RAR uncompression program"
  depends=('gcc-libs')
  cd "${srcdir}/${pkgbase}"

  install -Dm755 unrar "${pkgdir}/usr/bin/unrar"
  # install license
  install -Dm644 license.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_libunrar() {
  pkgdesc="Library and header file for applications that use libunrar"
  depends=('gcc-libs')
  cd "${srcdir}/libunrar"

  install -Dm755 libunrar.so.${pkgver} "${pkgdir}/usr/lib/libunrar.so.${pkgver}"
  install -Dm644 dll.hpp "${pkgdir}/usr/include/unrar/dll.hpp"
  ln -s libunrar.so.${pkgver} "${pkgdir}/usr/lib/libunrar.so.4"
  ln -s libunrar.so.${pkgver} "${pkgdir}/usr/lib/libunrar.so"
  # install license
  install -Dm644 license.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
