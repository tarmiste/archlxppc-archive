# $Id: PKGBUILD 145920 2012-01-04 01:26:04Z allan $
# x86 Maintainer: Dan McGee <dan@archlinux.org>
# x86 Maintainer: Allan McRae <allan@archlinux.org>

pkgname=valgrind
pkgver=3.7.0
pkgrel=2
pkgdesc="A tool to help find memory-management problems in programs"
arch=('ppc' 'i686' 'x86_64')
license=('GPL')
url="http://valgrind.org/"
depends=('glibc>=2.15' 'glibc<2.16' 'perl')
makedepends=('gdb')
options=('!emptydirs')
source=(http://valgrind.org/downloads/${pkgname}-${pkgver}.tar.bz2
        valgrind-3.7.0-glibc-2.15.patch)
md5sums=('a855fda56edf05614f099dca316d1775'
         'f534b79b358cdced0319b375aafde4d5')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  
  patch -Np1 -i ${srcdir}/valgrind-3.7.0-glibc-2.15.patch

  if [ "${CARCH}" = "x86_64" ]; then
    ./configure --prefix=/usr --mandir=/usr/share/man --enable-only64bit
  else
    ./configure --prefix=/usr --mandir=/usr/share/man
  fi
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
}
