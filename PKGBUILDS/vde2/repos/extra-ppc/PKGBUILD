# $Id$
# Contributor: Sergej Pupykin
# x86 Maintainer: Tobias Powalowski <tpowa@archlinux.org>
pkgname=vde2
pkgver=2.3.2
pkgrel=1
pkgdesc="Virtual Distributed Ethernet for emulators like qemu"
url="http://sourceforge.net/projects/vde/"
license=("GPL" "LGPL" "CUSTOM")
arch=('i686' 'x86_64' 'ppc')
depends=('bash' 'libpcap' 'openssl')
backup=(etc/conf.d/vde)
source=(http://downloads.sourceforge.net/vde/$pkgname-$pkgver.tar.bz2
        dhcpd.conf.sample
        iptables.rules.sample
        vde-config.sample
        vde-connection.sample
        vde.conf
        vde.rc)
install=vde2.install
options=(!libtool !makeflags)

build() {
  cd $srcdir/$pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --libexecdir=/usr/lib/vde2 \
             --enable-experimental
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver 
  make prefix=$pkgdir/usr sysconfdir=$pkgdir/etc libexecdir=$pkgdir/usr/lib/vde2 install
  install -D -m 755 ../vde.rc $pkgdir/etc/rc.d/vde
  install -D -m 644 ../vde.conf $pkgdir/etc/conf.d/vde
  install -D -m 644 ../vde-config.sample $pkgdir/etc/vde/vde-config.sample
  install -D -m 644 ../vde-connection.sample $pkgdir/etc/vde/vde-connection.sample
  install -D -m 644 ../dhcpd.conf.sample $pkgdir/usr/share/vde2/dhcpd.conf.sample
  install -D -m 644 ../iptables.rules.sample $pkgdir/usr/share/vde2/iptables.rules.sample
  # install slirp license
  install -D -m 644 COPYING.slirpvde $pkgdir/usr/share/licenses/vde2/COPYING.slirpvde
}

md5sums=('46fbc5f97f03dc517aa3b2c9d9ea6628'
         '7d9bc56d2e561d849e915000d1c0f269'
         'a920123fc620bcedbccb703a8d1bdc55'
         'cb8ace28e8efd4dad128be4be71b3b07'
         '63033c33565e2030541c5e05e9d9b063'
         'a22730f051f4840da4a3162a88ff8156'
         '6c7dc01bc2f039f0ff1682ee70d3d1da')
