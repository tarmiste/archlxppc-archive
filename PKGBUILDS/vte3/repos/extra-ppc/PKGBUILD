# $Id: PKGBUILD 142687 2011-11-12 18:46:20Z ibiru $
# x86 Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgbase=vte3
pkgname=(vte3 vte-common)
pkgver=0.30.1
pkgrel=2
pkgdesc="Virtual Terminal Emulator widget for use with GTK3"
arch=('ppc' 'i686' 'x86_64')
license=('LGPL')
options=('!libtool' '!emptydirs')
makedepends=('intltool' 'gobject-introspection' 'gtk3')
url="http://www.gnome.org"
source=(http://ftp.gnome.org/pub/GNOME/sources/vte/0.30/vte-$pkgver.tar.xz
make_alt_work.patch::http://git.gnome.org/browse/vte/patch/?id=b73782a28894e25ed146271f9d6c6775a6836199)
sha256sums=('bbe421de3015935c22f685eea0eb8be2c1fecd5021c1f822996b8f0a282f6b64'
            '8307623b13bcaf214cc25207ee7afaf65575d334623460311c24437fbbae14d3')

build() {
  cd "$srcdir/vte-$pkgver"
  #warning: type-punning to incomplete type might break strict-aliasing rules
  export CFLAGS="$CFLAGS -fno-strict-aliasing"

  patch -Np1 -R -i "$srcdir/make_alt_work.patch"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --libexecdir=/usr/lib/vte \
      --localstatedir=/var --disable-static \
      --enable-introspection
  make
}

package_vte3(){
  depends=('gtk3' 'vte-common')
  cd "$srcdir/vte-$pkgver"
  make DESTDIR="$pkgdir" install

  rm "$pkgdir"/usr/lib/vte/gnome-pty-helper
}

package_vte-common() {
  pkgdesc="Common files used by vte and vte3"
  cd "$srcdir/vte-$pkgver"

  make -C gnome-pty-helper DESTDIR="$pkgdir" install
}
