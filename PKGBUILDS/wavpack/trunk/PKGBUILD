# $Id: PKGBUILD 146400 2012-01-10 20:33:06Z andrea $
# x86 Maintainer:
# Contributor: François Charette <firmicus ατ gmx δοτ net> 
# Contributor: Shinlun Hsieh <yngwiexx@yahoo.com.tw>
# Contributor: Michal Hybner <dta081@gmail.com>

pkgname=wavpack
pkgver=4.60.1
pkgrel=2
pkgdesc="Audio compression format with lossless, lossy, and hybrid compression modes"
arch=('ppc' 'i686' 'x86_64')
url="http://www.wavpack.com/"
license=('custom')
depends=('glibc')
options=('!libtool')
source=("http://www.wavpack.com/${pkgname}-${pkgver}.tar.bz2")
md5sums=('7bb1528f910e4d0003426c02db856063')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --prefix=/usr \
    --disable-static
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install

  install -Dm644 license.txt \
    ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
