# $Id: PKGBUILD,v 1.3 2008/09/30 13:52:59 kth5 Exp $
# i686 Maintainer: Jeff Mickey <jeff@archlinux.org>
# i686 Contributor: Jeffrey 'jf' Lim <jfs.world@gmail.com>
pkgname=wmii
pkgver=3.6
pkgrel=1
pkgdesc="Next generation WMI (Window Manager Improved 2)"
arch=('i686' 'x86_64' 'ppc')
license=('MIT')
url="http://wmii.suckless.org/"
depends=('glibc' 'libx11' 'libixp>=0.4' 'dmenu' 'libxt')
makedepends=('xorg-headers')
source=(http://code.suckless.org/dl/wmii/${pkgname}-${pkgver}.tar.gz 
        wmii.desktop
        01-x-terminal-emulator.patch
        02-cflags.patch
        03-font.patch
        04-manpage.patch)
options=('!makeflags' '!distcc')

build()
{
  cd ${srcdir}/${pkgname}-${pkgver}
  patch -Np1 -i ${srcdir}/01-x-terminal-emulator.patch || return 1
  patch -Np1 -i ${srcdir}/02-cflags.patch || return 1
  patch -Np1 -i ${srcdir}/03-font.patch || return 1
  patch -Np1 -i ${srcdir}/04-manpage.patch || return 1

  sed -i \
        -e "/^PREFIX/s|=.*|= ${startdir}/pkg/usr|" \
		-e "/^ETC/s|=.*|= ${startdir}/pkg/etc|" \
		-e "/^LIBDIR/s|=.*|= /usr/lib|" \
		config.mk || return 1

  make -j1 || return 1
  make -j1 DESTDIR=${pkgdir} install

  mkdir -p ${pkgdir}/etc/X11/sessions
  install -m644 ../wmii.desktop ${pkgdir}/etc/X11/sessions/


  install -m644 -D ./LICENSE \
                    ${pkgdir}/usr/share/licenses/wmii/LICENSE

  # Rid paths of temporary install directory.
  sed -i -e "s|${pkgdir}||g" "${pkgdir}/usr/bin/wmiistartrc"
}

md5sums=('9d17a09871fada998b4d989d9318bbf5'
         '997d01cd35931b548fa7e6d1011ff6d4'
         '40fba0cdb89643ccfc42decd67378ee3'
         '26588596bf3b435a44939b0ee48b05bd'
         'a1f5cf442f20ab7d56c05c05e885fed0'
         '015c2b94c8c687b4f6c9611d7fda8c78')
