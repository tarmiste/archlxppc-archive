# $Id$
# x86 Maintainer: Jan de Groot <jgc@archlinux.org>
# x86 Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgname=xcb-util-image
pkgver=0.3.8
pkgrel=1
pkgdesc="Utility libraries for XC Binding - Port of Xlib's XImage and XShmImage functions"
arch=('i686' 'x86_64' 'ppc')
url="http://xcb.freedesktop.org"
license=('custom')
depends=('libxcb>=1.7' 'xcb-util>=0.3.8')
makedepends=('xorg-util-macros')
options=('!libtool')
source=("http://xcb.freedesktop.org/dist/${pkgname}-${pkgver}.tar.bz2"
        'LICENSE')
md5sums=('245a96041f373b05764148e52d2e0211'
         '2672c2e72dedb40f773e989dd622f298')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --disable-static
  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make check
}

package() { 
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -D -m644 "${srcdir}/LICENSE" \
    "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
