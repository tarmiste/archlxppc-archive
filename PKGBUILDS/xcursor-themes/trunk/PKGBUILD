# $Id$
#Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xcursor-themes
pkgver=1.0.3
pkgrel=2
pkgdesc="X.org Cursor themes"
arch=(any)
url="http://xorg.freedesktop.org/"
license=('MIT')
makedepends=('pkgconfig' 'xorg-xcursorgen' 'xorg-util-macros')
source=(${url}/releases/individual/data/${pkgname}-${pkgver}.tar.bz2)
md5sums=('ba21aad0b353f1881f5069e423a44587')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
