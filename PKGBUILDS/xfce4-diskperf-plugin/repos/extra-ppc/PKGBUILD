# $Id$
# x86 Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: Aurelien Foret <orelien@chez.com>

pkgname=xfce4-diskperf-plugin
pkgver=2.3.0
pkgrel=2
pkgdesc="Plugin for the Xfce4 panel displaying instant disk/partition performance"
arch=('i686' 'x86_64' 'ppc')
license=('custom')
url="http://goodies.xfce.org/projects/panel-plugins/xfce4-diskperf-plugin"
groups=('xfce4-goodies')
depends=('xfce4-panel' 'libxfcegui4') # 'libxfce4ui')
makedepends=('intltool')
source=(http://archive.xfce.org/src/panel-plugins/${pkgname}/2.3/${pkgname}-${pkgver}.tar.bz2)
md5sums=('c6ece8123c762ee203cbc6fd5450b503')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  # Work around DSO linking issue
  LDFLAGS+=' -Wl,--copy-dt-needed-entries'

  ./configure --prefix=/usr \
	--sysconfdir=/etc \
	--libexecdir=/usr/lib \
	--localstatedir=/var \
	--disable-static \
	--disable-debug
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
  install -Dm644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/COPYING
}
