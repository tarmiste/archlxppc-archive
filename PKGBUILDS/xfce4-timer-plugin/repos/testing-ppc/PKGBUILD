# $Id$
# x86 Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: Tobias Kieslich <tobias (at) archlinux.org>

pkgname=xfce4-timer-plugin
pkgver=0.6.4
pkgrel=2
pkgdesc="plugin to track time for the Xfce4 panel"
arch=('i686' 'x86_64' 'ppc')
license=('GPL2')
url="http://goodies.xfce.org/projects/panel-plugins/xfce4-timer-plugin"
groups=('xfce4-goodies')
depends=('xfce4-panel' 'libxfcegui4')
makedepends=('intltool')
options=('!libtool')
install=xfce4-timer-plugin.install
source=(http://archive.xfce.org/src/panel-plugins/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.bz2)
md5sums=('c2f9e113dcda742cd1559129b79f609b')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
	--sysconfdir=/etc \
	--libexecdir=/usr/lib/ \
	--localstatedir=/var \
	--disable-static
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
