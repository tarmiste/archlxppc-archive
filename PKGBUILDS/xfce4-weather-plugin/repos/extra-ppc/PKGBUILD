# $Id: PKGBUILD 142430 2011-11-09 18:10:30Z foutrelis $
# x86 Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Suzy Williams <suzanne.williams3@verizon.net>

pkgname=xfce4-weather-plugin
pkgver=0.7.4
pkgrel=2
pkgdesc="A weather plugin for the Xfce4 panel"
arch=('ppc' 'i686' 'x86_64')
url="http://goodies.xfce.org/projects/panel-plugins/xfce4-weather-plugin"
license=('GPL2')
groups=('xfce4-goodies')
depends=('xfce4-panel' 'libxfcegui4' 'libxml2' 'hicolor-icon-theme')
makedepends=('intltool')
options=('!libtool')
install=$pkgname.install
source=(http://archive.xfce.org/src/panel-plugins/$pkgname/0.7/$pkgname-$pkgver.tar.bz2)
sha256sums=('3f76207b8a845d15bfec6825bd5300aedd086c455259c4dd8670a89a3c8ab382')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # Switch to a working license key
  # https://bugs.archlinux.org/task/26815
  sed -i \
    -e 's/^#define PARTNER_ID.*/#define PARTNER_ID "1003666583"/' \
    -e 's/^#define LICENSE_KEY.*/#define LICENSE_KEY "4128909340a9b2fc"/' \
    panel-plugin/weather.h

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --disable-static \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
