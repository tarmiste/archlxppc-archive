# $Id$
# x86 Maintainer: Paul Mattal <paul@archlinux.org>
pkgname=xfsprogs
pkgver=3.1.8
pkgrel=1
pkgdesc="XFS filesystem utilities"
arch=('i686' 'x86_64' 'ppc')
license=('LGPL')
url="http://oss.sgi.com/projects/xfs/"
groups=('base')
depends=('util-linux-ng>=2.16')
options=('!makeflags' '!libtool')
# We mirror the sources as upstream tends to move them once a new release is out
source=("http://mirrors.kernel.org/archlinux/other/xfsprogs/${pkgname}-${pkgver}.tar.gz")
md5sums=('f70b2e7200d4c29f0af1cf70e7be1db6')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  export OPTIMIZER="-march=${CARCH/_/-} -O1"
  export DEBUG=-DNDEBUG
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DIST_ROOT="${pkgdir}" install install-dev
  chown -R root $pkgdir
  chgrp -R root $pkgdir
}
