# $Id$
# x86 Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xorg-luit
pkgver=1.1.1
pkgrel=1
pkgdesc="Filter that can be run between an arbitrary application and a UTF-8 terminal emulator"
arch=(i686 x86_64 'ppc')
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('libfontenc')
groups=('xorg' 'xorg-apps')
makedepends=('xorg-util-macros')
source=(http://xorg.freedesktop.org/archive/individual/app/luit-${pkgver}.tar.bz2)
sha1sums=('3130c14d7267cecce0ba2280643844b48cca49b0')

build() {
  cd "${srcdir}/luit-${pkgver}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/luit-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
